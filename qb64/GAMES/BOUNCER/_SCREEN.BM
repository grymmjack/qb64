SUB screen_init 
    DPRINT "screen_init", DEBUG_AVG
    screen_reset
    __SCREEN.BG_KOLOR%     = 0
    __SCREEN.FG_KOLOR%     = 0
    __SCREEN.W%            = _WIDTH
    __SCREEN.H%            = _HEIGHT
    __SCREEN.BPP%          = _PIXELSIZE
    __SCREEN.VISIBLE_PAGE% = 0
    __SCREEN.ACTIVE_PAGE%  = 0
    _SCREENMOVE _MIDDLE
END SUB

SUB screen_reset
    DPRINT "screen_reset", DEBUG_AVG
    SCREEN 0,,0,0
    _BLINK OFF
    _CONTROLCHR OFF
    IF CFG.FULLSCREEN_SMOOTH% = TRUE THEN
        IF CFG.FULLSCREEN% = TRUE THEN _FULLSCREEN _SQUAREPIXELS, _SMOOTH
    ELSE
        IF CFG.FULLSCREEN% = TRUE THEN _FULLSCREEN _SQUAREPIXELS
    END IF
END SUB

SUB screen_clear
    DPRINT "screen_clear", DEBUG_AVG
    IF CFG.RANDOM_BG% = TRUE THEN __SCREEN.BG_KOLOR% = rand_in_range(0, 7)
    screen_set_active_page GAME_SCREEN
    COLOR ,__SCREEN.BG_KOLOR%
    CLS
END SUB

SUB screen_set_active_page (page%)
    SCREEN ,,page%
    __SCREEN.ACTIVE_PAGE% = page%
END SUB

SUB screen_set_visible_page (page%)
    SCREEN ,,,page%
    __SCREEN.VISIBLE_PAGE% = page%
END SUB

SUB screen_set_both_pages (page%)
    SCREEN ,,page%,page%
    __SCREEN.ACTIVE_PAGE%  = page%
    __SCREEN.VISIBLE_PAGE% = page%
END SUB
