10 REM ANAGRAM USER
20 REM Written by A.Sill
30 REM FOR WROX PRESS
40 REM (c) 1992
50 REM
60 NUMBER% = 152: DIM WORD$(NUMBER%)
70 SCREEN 9: CLS
80 A$ = "ANAGRAM USER"
90 COLOR 2
100 LOCATE 2, (80 - LEN(A$)) / 2: PRINT A$
110 COLOR 10
120 COLOR 10: LOCATE 5, 1: PRINT "     This program shows BASIC's great aptitude for text handling, making great  use of the LEFT, RIGHT and MID functions anagrams can be easily produced. The"
130 PRINT "program even includes a sort of game where you can guess the word from it's     anagram version, though they are very tough !."
140 RAND = 0
150 LOCATE 20, 33: COLOR 12: PRINT "Press any key"
160 A$ = ""
170 A$ = INKEY$: RAND = RAND + .1: IF A$ = "" THEN 170
180 RAND = RAND - VAL(RIGHT$(TIME$, 2))
190 RANDOMIZE RAND
200 REM
210 GOSUB 840
220 CLS
230 LINE (1, 1)-(639, 349), 15, B
240 LINE (3, 2)-(637, 348), 7, B
250 LINE (5, 3)-(635, 347), 8, B
260 COLOR 15: LOCATE 2, 34: PRINT "ANAGRAM USER"
270 COLOR 7: LOCATE 5, 25: PRINT "1    -    ANAGRAM GUESSER"
280         LOCATE 6, 25: PRINT "2    -    CREATE AN ANAGRAM"
290         LOCATE 7, 25: PRINT "3    -    PERMUTATIONS"
300         LOCATE 8, 25: PRINT "4    -    QUIT PROGRAM"
310 A$ = INKEY$: IF A$ = "" THEN 310
320 IF A$ = "1" THEN GOTO 370
330 IF A$ = "2" THEN GOTO 520
340 IF A$ = "3" THEN GOTO 630
350 IF A$ = "4" THEN GOTO 770
360 GOTO 310
370 REM ********** ANAGRAM GUESSER
380 LOCATE 11, 10: PRINT "Guess this anagram :"
390 X = INT(RND * NUMBER%): ANAGRAM$ = WORD$(X)
400 FOR A = 1 TO 50
410 B = INT(RND * LEN(ANAGRAM$)) + 1: C = INT(RND * LEN(ANAGRAM$)) + 1
420 IF B = C THEN GOTO 410
430 A$ = MID$(ANAGRAM$, B, 1): MID$(ANAGRAM$, B, 1) = MID$(ANAGRAM$, C, 1)
440 MID$(ANAGRAM$, C, 1) = A$
450 NEXT A
460 COLOR 10: LOCATE 13, 35: PRINT ANAGRAM$
470 COLOR 15: LOCATE 15, 33: INPUT ANSWER$
480 IF ANSWER$ = WORD$(X) THEN COLOR 14: LOCATE 18, 33: PRINT "THAT'S RIGHT !"
490 IF ANSWER$ <> WORD$(X) THEN COLOR 7: LOCATE 18, 33: PRINT "UNLUCKY MATE !": LOCATE 20, 30: PRINT "It was : "; WORD$(X)
500 WHILE INKEY$ = "": WEND
510 GOTO 220
520 REM ********** CREATE AN ANAGRAM
530 LOCATE 11, 10: INPUT "Enter the text to be anagramized : "; TEXT$
540 FOR A = 1 TO 50
550 B = INT(RND * LEN(TEXT$)) + 1: C = INT(RND * LEN(TEXT$)) + 1
560 IF B = C THEN GOTO 550
570 A$ = MID$(TEXT$, B, 1): MID$(TEXT$, B, 1) = MID$(TEXT$, C, 1)
580 MID$(TEXT$, C, 1) = A$
590 NEXT A
600 COLOR 12: LOCATE 13, 30: PRINT "How about : "; TEXT$
610 WHILE INKEY$ = "": WEND
620 GOTO 220
630 REM ********** PERMUTATIONS
640 LOCATE 11, 10: INPUT "Enter the text for permutations : "; TEXT$
650 IF LEN(TEXT$) > 15 THEN GOTO 640
660 FOR D = 1 TO 5: FOR E = 1 TO 10
670 FOR A = 1 TO 1' *** INCREASE THIS TO CHANGE THE GRADING OF PERMUTATIONS
680 B = INT(RND * LEN(TEXT$)) + 1: C = INT(RND * LEN(TEXT$)) + 1
690 IF B = C THEN GOTO 680
700 A$ = MID$(TEXT$, B, 1): MID$(TEXT$, B, 1) = MID$(TEXT$, C, 1)
710 MID$(TEXT$, C, 1) = A$
720 NEXT A
730 LOCATE 12 + E, D * (LEN(TEXT$) + 1): PRINT TEXT$
740 NEXT E, D
750 WHILE INKEY$ = "": WEND
760 GOTO 220
770 REM ********** QUIT PROGRAM
780 LOCATE 11, 10: PRINT "1. Return to DOS"
790 LOCATE 13, 10: PRINT "2. Carry on with ANAGRAMS"
800 A$ = INKEY$: IF A$ = "" THEN 800
810 IF A$ = "1" THEN SYSTEM
820 IF A$ = "2" THEN GOTO 220
830 GOTO 800
840 REM ********** READ IN DATA WORDS
850 FOR A = 1 TO NUMBER%
860 READ WORD$(A)
870 NEXT A
880 DATA AEROPLANE,DIPLOMACY,MAGNITUDE,PENTHOUSE,SCULPTURE,STAMPEDE
890 DATA LANGUAGE,HOLOGRAPH,DIFFERENT,CORPORAL,BROADCAST,ELEPHANT
900 DATA GENERATE,MINOTAUR,MECHANIC,MEASURED,FOOTBALLER,HESITATE
910 DATA HAWTHORN,HAMBURGER,CHRISTMAS,HOLIDAY,GREAT BRITAIN,DICTIONARY
920 DATA OLYMPICS,TELEVISION,COMPUTER,MANAGEMENT,BUFFALO,NOVEMBER
930 DATA WHIRLWIND,PROPAGANDA,JOHN MAJOR,FRANCE,MOVEMENT,CHILDREN
940 DATA BUSINESS,ENGLAND,AMERICA,SHAKESPEARE,EUROPE,COLLEGE,FRACTAL
950 DATA JAVELIN,AMSTRAD,COLUMBUS,RED INDIAN,JRR TOLKIEN,CHINESE
960 DATA IRELAND,LENNOX LEWIS,BILLY THE KID,ENVIRONMENT,HARMONIC
970 DATA DONKEY,CRICKET,LOS ANGELES,PRINTER,HARD DISK,ANCIENT,RITUAL
980 DATA TRIBES,GUILTY,BANNERS,PENCIL,MUSICALLY,PARCEL,UTILITY
990 DATA ATOMIC,FANTASTIC,MAGAZINE,BUILDING,HELICOPTER,TRAFFIC
1000 DATA PRACTICAL,PLAYBOY,EDUCATION,SCANNER,JOYSTICK,COCKTAIL
1010 DATA PORTABLE,ARTIFICIAL,WARRANTY,EXPLICIT,ADVISORY,GUITAR
1020 DATA EXAMINATION,SITUATION,QUALIFY,PUBLISH,LEAGUE,COLLECTION
1030 DATA INVEST,HANGOVER,COMPETITION,TENNIS,GRAPEVINE,DIRECTOR
1040 DATA LECTURER,MOTHER,WILDLIFE,MULTIMEDIA,VEGETABLE,DISPLAY
1050 DATA SUBSCRIBE,BENEATH,SIMULATION,ANIMATION,PACKAGE,PROTECT
1060 DATA INSTRUCT,HOBBIES,ASTRONAUT,HERITAGE,PERSONAL,CREATOR
1070 DATA ADMIRAL,GALACTIC,WIZARD,PONTOON,DUNGEON,WESTERN
1080 DATA DRAGON,COFFEE,EMULATE,DINOSAUR,ELECTRONIC,PROCLAIM
1090 DATA RELEASE,POPULAR,ASSAULT,FIGHTER,TECHNIQUE,REBELLION
1100 DATA TORPEDO,PLATFORM,REINFORCE,SHORTAGE,HAMMER,EMPEROR
1110 DATA CRYSTAL,ADVENTURE,DEVELOPMENT,SOMEWHERE,LONGEVITY,TRIANGLE
1120 DATA OVERALL,VETERAN,INSPIRATION,ATMOSPHERE,FINGERNAIL,FLYING
1130 RETURN

