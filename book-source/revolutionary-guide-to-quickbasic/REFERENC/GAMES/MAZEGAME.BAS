10 REM THE MAZE GAME
20 REM Written by A.Sill
30 REM FOR WROX PRESS
40 REM (c) 1992
50 REM
60 DIM BRANCH(30, 1), MAZE%(40, 25)
70 SCREEN 9: CLS
80 A$ = "THE MAZE GAME"
90 COLOR 2
100 LOCATE 2, (80 - LEN(A$)) / 2: PRINT A$
110 COLOR 10: LOCATE 5, 1: PRINT "     This game is based on the GRAPHICS 8 : MAZE CREATOR program, where you haveto guide a man (yellow pixel) through the maze in the fastest time possible."
120 PRINT "     This is an infuriating game and has a record time of 15 seconds by myself. You'll do well to beat it."
130 RAND = 0
140 LOCATE 20, 33: COLOR 12: PRINT "Press any key"
150 A$ = ""
160 A$ = INKEY$: RAND = RAND + .1: IF A$ = "" THEN 160
170 RAND = RAND - VAL(RIGHT$(TIME$, 2))
180 RANDOMIZE RAND
190 REM
200 SCREEN 7: CLS
210 PRINT "PLEASE WAIT 10 SECONDS TO CALCULATE THE SCREEN LAYOUT. THANK YOU SO MUCH BECAUSEWITHOUT THE MAZE THERE IS NO GAME."
220 MX = 38: MY = 22
230 LEFT = MX * MY / 4 - 1: E = 5
240 IF MX > 9 AND MY > 9 THEN E = 0
250 BRAN = 0
260 FOR F = 0 TO MX: MAZE%(F, 0) = 2: MAZE%(F, MY) = 2: NEXT F
270 FOR F = 1 TO MY - 1: MAZE%(0, F) = 2: MAZE%(MX, F) = 2: NEXT F
280 FOR F = 2 TO MY - 2 STEP 2
290 FOR G = 2 TO MX - 2 STEP 2
300 MAZE%(G, F) = 2
310 NEXT G, F
320 MAZE%(MX - 1, 0) = 1
330 MAZE%(1, MY) = 1
340 X = MX - 1: Y = 1: H = 2: LOOPY = 0: CX = -1: CY = 1
350 WHILE LOOPY = 0
360 MAZE%(X, Y) = 1
370 GOSUB 660 ' ****** CREATE THE MAZE
380 WEND
390 GOSUB 1190 ' ****** DRAW THE MAZE ON THE SCREEN
400 GOTO 1280
410 REM ****** EXAMINE THE IMMEDIATE AREA OF MAZE
420 C$ = ""
430 IF X - 2 > 0 THEN IF MAZE%(X - D, Y) = 0 THEN C$ = C$ + "l"
440 IF X + 2 < MX THEN IF MAZE%(X + D, Y) = 0 THEN C$ = C$ + "r"
450 IF Y - 2 > 0 THEN IF MAZE%(X, Y - D) = 0 THEN C$ = C$ + "u"
460 IF Y + 2 < MY THEN IF MAZE%(X, Y + D) = 0 THEN C$ = C$ + "d"
470 C = LEN(C$)
480 RETURN
490 REM ****** END PATH
500 D = 1: GOSUB 410' ***** SEARCH THE SURROUNDING AREA
510 WHILE C > 0: GOSUB 1030: WEND
520 IF LEFT = 0 THEN LOOPY = 1: RETURN
530 IF BRAN = 0 THEN GOSUB 570: RETURN
540 BRAN = BRAN - 1
550 X = BRANCH(BRAN, 0): Y = BRANCH(BRAN, 1)
560 RETURN
570 REM ****** SEARCH FOR A SECTION OF THE PATH
580 X = CX: Y = CY: FLAG = 0
590 WHILE FLAG = 0
600 X = X + 2
610 IF X > MX THEN X = 1: Y = Y + 2
620 IF Y > MY THEN Y = 1
630 IF MAZE%(X, Y) = 1 THEN GOSUB 1120
640 WEND
650 RETURN
660 REM ****** CREATE THE MAZE
670 R = INT(RND * 100) + 1
680 D = 2: GOSUB 410' ****** EXAMINE THE SURROUNDING AREA OF MAZE
690 IF C = 0 THEN GOSUB 490: RETURN' ****** END THE CURRENT PATH
700 IF R < E THEN GOSUB 490: RETURN' ******   "        "     "
710 IF R < 25 AND C > 1 AND BRAN < 31 THEN GOSUB 740' ***** MAKE A BRANCH
720 GOSUB 880 ' ****** MOVE
730 RETURN
740 REM ****** MAKE A BRANCHING
750 Z = INT(RND * C) + 1
760 B$ = MID$(C$, Z, 1)
770 IF B$ = "l" THEN MAZE%(X - 1, Y) = 1: NX = X - 2: NY = Y: GOSUB 460' ***** BRANCH S.H.
780 IF B$ = "r" THEN MAZE%(X + 1, Y) = 1: NX = X + 2: NY = Y: GOSUB 460' ***** BRANCH S.H.
790 IF B$ = "u" THEN MAZE%(X, Y - 1) = 1: NX = X: NY = Y - 2: GOSUB 460' ***** BRANCH S.H.
800 IF B$ = "d" THEN MAZE%(X, Y + 1) = 1: NX = X: NY = Y + 2: GOSUB 460' ***** BRANCH S.H.
810 C$ = LEFT$(C$, Z - 1) + MID$(C$, Z + 1, 4): C = C - 1
820 RETURN
830 REM ****** BRANCH STACK HANDLER
840 MAZE%(NX, NY) = 1
850 BRANCH(BRAN, 0) = NX: BRANCH(BRAN, 1) = NY
860 BRAN = BRAN + 1: LEFT = LEFT - 1
870 RETURN
880 REM ****** MOVE
890 WHILE C > 1
900 GOSUB 1030 ' ****** FILL IN WALLS, BREAK THEM
910 WEND
920 S$ = C$: H = 1
930 GOSUB 1030: H = 2' ***** MAKE A PATH
940 D = 1: GOSUB 410' ***** SEARCH SURROUNDINGS
950 WHILE C > 0: GOSUB 1030: WEND
960 C$ = S$
970 IF C$ = "l" THEN X = X - 2
980 IF C$ = "r" THEN X = X + 2
990 IF C$ = "u" THEN Y = Y - 2
1000 IF C$ = "d" THEN Y = Y + 2
1010 LEFT = LEFT - 1
1020 RETURN
1030 REM ****** FILL IN WALLS, BREAK THEM, CLOSE OFF EXITS
1040 Z = INT(RND * C) + 1
1050 B$ = MID$(C$, Z, 1)
1060 IF B$ = "l" THEN MAZE%(X - 1, Y) = H
1070 IF B$ = "r" THEN MAZE%(X + 1, Y) = H
1080 IF B$ = "u" THEN MAZE%(X, Y - 1) = H
1090 IF B$ = "d" THEN MAZE%(X, Y + 1) = H
1100 C$ = LEFT$(C$, Z - 1) + MID$(C$, Z + 1, 4): C = C - 1
1110 RETURN
1120 REM ***** IS IT A SPACE NEXT ?
1130 D = 2: GOSUB 410' ***** SEARCH IMMEDIATE AREA
1140 IF C = 0 THEN RETURN
1150 CX = X: CY = Y: H = 1
1160 GOSUB 1030: H = 2: FLAG = 1: C$ = B$
1170 GOSUB 970
1180 RETURN
1190 REM ****** DRAW MAZE ON THE SCREEN
1200 CLS
1210 FOR F = 0 TO MY: FOR G = 0 TO MX
1220 A = MAZE%(G, F)
1230 IF A = 1 THEN PRINT " "; :  ELSE PRINT "Û";
1240 NEXT G
1250 PRINT
1260 NEXT F
1270 RETURN
1280 REM GAME
1290 X = 20: Y = 189: COUNTER = 0
1300 PSET (X, Y), 14
1310 WHILE Y > 8
1320 COUNTER = COUNTER + 1
1330 A$ = INKEY$: IF A$ = "" THEN 1330
1340 IF RIGHT$(A$, 1) = "H" AND POINT(X, Y - 3) <> 15 THEN Y = Y - 3
1350 IF RIGHT$(A$, 1) = "K" AND POINT(X - 3, Y) <> 15 THEN X = X - 3
1360 IF RIGHT$(A$, 1) = "P" AND POINT(X, Y + 3) <> 15 THEN Y = Y + 3
1370 IF RIGHT$(A$, 1) = "M" AND POINT(X + 3, Y) <> 15 THEN X = X + 3
1380 LINE -(X, Y), 14
1390 WEND
1400 COLOR 2: LOCATE 8, 15: PRINT "THAT  TOOK"
1410 LOCATE 10, 15: PRINT COUNTER / 8; "SECONDS"
1420 A$ = ""
1430 FOR A = 1 TO 5000: NEXT A
1440 WHILE INKEY$ = "": WEND
1450 REM ********* RETURN TO MENU POSER
1460 SCREEN 9
1470 CLS
1480 COLOR 12: PRINT : PRINT : PRINT "          Do you want to 1. Run again or": PRINT "                         2. Return to DOS ?."
1490 LOCATE 3, 26: COLOR 7: PRINT "1"
1500 LOCATE 4, 26: PRINT "2"
1510 A$ = INKEY$: IF A$ = "" THEN GOTO 1510
1520 IF A$ = "1" THEN CLEAR : RUN
1530 IF A$ = "2" THEN CLS : SYSTEM
1540 GOTO 1510

