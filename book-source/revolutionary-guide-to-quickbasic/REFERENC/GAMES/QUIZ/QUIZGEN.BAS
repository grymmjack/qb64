10 REM QUIZ FILE MAKER
20 REM Written by A.Sill
30 REM FOR WROX PRESS
40 REM (c) 1992
50 REM
60 DIM X$(300), Y$(300)
70 SCREEN 9: CLS
80 A$ = "QUIZ FILE MAKER"
90 COLOR 2
100 LOCATE 2, (80 - LEN(A$)) / 2: PRINT A$
110 COLOR 10: LOCATE 5, 1: PRINT "     This program is designed to help in the creation of quizzes for the quiz ofTHE QUIZ. It has been used to create the quizzes supplied with"
120 PRINT "these packages and it can also amend the questions in an existing quiz or it canadd additional questions to an existing quiz."
130 RAND = 0
140 LOCATE 20, 33: COLOR 12: PRINT "Press any key"
150 A$ = ""
160 A$ = INKEY$: RAND = RAND + .1: IF A$ = "" THEN 160
170 RAND = RAND - VAL(RIGHT$(TIME$, 2))
180 RANDOMIZE RAND
190 REM
200 SCREEN 7: CLS
210 COLOR 14: LOCATE 2, 12: PRINT "QUIZ FILE CREATOR"
220 LINE (1, 1)-(318, 198), 14, B
230 LINE (2, 2)-(319, 199), 6, B
240 COLOR 12: LOCATE 5, 6: PRINT "1---- CREATE A NEW QUIZ FILE"
250 LOCATE 7, 6: PRINT "2---- ADD TO A FILE"
260 LOCATE 9, 6: PRINT "3---- AMEND A QUIZ FILE"
270 LOCATE 11, 6: PRINT "4---- QUIT"
280 COLOR 7: LOCATE 16, 15: PRINT "Press a key"
290 A$ = INKEY$: IF A$ = "" THEN 290
300 IF A$ = "4" THEN CLEAR : SYSTEM
305 IF A$ = "2" THEN GOTO 700
310 IF A$ = "1" THEN GOTO 320
315 IF A$ = "3" THEN GOTO 900
320 SCREEN 7: CLS : COLOR 14: LOCATE 2, 12: PRINT "QUIZ FILE CREATOR"
330 LINE (1, 1)-(318, 198), 14, B
340 LINE (2, 2)-(319, 199), 6, B
350 COLOR 2: LOCATE 5, 2: PRINT "Enter Name of file :"; : COLOR 10: INPUT F$
360 IF F$ = "" OR LEN(F$) > 8 THEN 320
370 COLOR 2: LOCATE 7, 2: PRINT "Enter the uniform question for each"
380 LOCATE 8, 2: PRINT "question in the quiz :"
390 COLOR 10: LOCATE 9, 4: INPUT QUESTION$
400 IF LEN(QUESTION$) > 30 THEN 390
410 COLOR 2: LOCATE 11, 2: PRINT "Enter the total number of questions"
420 LOCATE 12, 2: PRINT "in the quiz file :";
430 COLOR 10: INPUT NUMBER
440 IF NUMBER < 4 OR NUMBER > 300 THEN 410
450 COLOR 15: LOCATE 20, 2: PRINT "Is this O.K. (Y)es or (N)o ?"
460 A$ = INKEY$: IF A$ = "" THEN 460
470 IF A$ = "Y" OR A$ = "y" THEN GOTO 500
480 IF A$ = "N" OR A$ = "n" THEN GOTO 200
490 GOTO 460
500 SCREEN 9: CLS
510 COLOR 6
520 OPEN F$ + ".QIZ" FOR OUTPUT AS #1
530 PRINT #1, QUESTION$
540 PRINT #1, NUMBER
550 FOR A = 1 TO NUMBER
555 IF X$(A) = "" AND Y$(A) = "" THEN 560 ELSE 580
560 COLOR 6: PRINT "Enter question number"; A; : COLOR 14: INPUT X$(A)
570 COLOR 6: PRINT "Enter answer :"; : COLOR 14: INPUT Y$(A)
580 PRINT #1, X$(A)
590 PRINT #1, Y$(A)
600 NEXT A
610 CLOSE
620 PRINT : COLOR 7: PRINT "Quiz file has been created"
630 WHILE INKEY$ = "": WEND
640 CLEAR : GOTO 200
700 REM **** add records to a file
705 SCREEN 7: CLS : COLOR 14: LOCATE 2, 9: PRINT "ADD TO AN EXISTING FILE"
710 LINE (1, 1)-(318, 198), 14, B
715 LINE (2, 2)-(319, 199), 6, B
720 COLOR 2: LOCATE 5, 2: PRINT "Enter filename :"; : COLOR 10: INPUT F$
725 IF F$ = "" OR LEN(F$) > 8 THEN 700
730 OPEN F$ + ".QIZ" FOR INPUT AS #1
735 INPUT #1, QUESTION$
740 INPUT #1, NUMBER
745 FOR A = 1 TO NUMBER
750 INPUT #1, X$(A)
755 INPUT #1, Y$(A)
760 NEXT A
765 CLOSE
770 LOCATE 7, 2: PRINT "QUESTION :"; QUESTION$
775 LOCATE 9, 2: PRINT "NUMBER OF QUESTIONS IS :"; NUMBER
780 COLOR 7: LOCATE 11, 2: PRINT "ADD SOME MORE QUESTIONS (Y)es/(N)o ?"
785 A$ = INKEY$: IF A$ = "" THEN 785
790 IF A$ = "Y" OR A$ = "y" THEN 805
795 IF A$ = "N" OR A$ = "n" THEN GOTO 200
800 GOTO 785
805 COLOR 10: LOCATE 13, 2: PRINT "HOW MANY MORE :"; : COLOR 2: INPUT EXTRA
810 NUMBER = NUMBER + EXTRA
815 GOTO 500
900 REM AMEND A QUIZ FILE
905 SCREEN 7: CLS : COLOR 14: LOCATE 2, 9: PRINT "AMEND AN EXISTING FILE"
910 LINE (1, 1)-(318, 198), 14, B
915 LINE (2, 2)-(319, 199), 6, B
920 COLOR 2: LOCATE 5, 2: PRINT "Enter filename :"; : COLOR 10: INPUT F$
925 IF F$ = "" OR LEN(F$) > 8 THEN 900
930 OPEN F$ + ".QIZ" FOR INPUT AS #1
935 INPUT #1, QUESTION$
940 INPUT #1, NUMBER
945 FOR A = 1 TO NUMBER
950 INPUT #1, X$(A)
955 INPUT #1, Y$(A)
960 NEXT A
965 CLOSE
970 LOCATE 7, 2: PRINT "QUESTION :"; QUESTION$
975 LOCATE 9, 2: PRINT "NUMBER OF QUESTIONS IS :"; NUMBER
980 COLOR 7: LOCATE 11, 2: PRINT "AMEND SOME QUESTIONS (Y)es/(N)o ?"
985 A$ = INKEY$: IF A$ = "" THEN 985
990 IF A$ = "Y" OR A$ = "y" THEN 1005
995 IF A$ = "N" OR A$ = "n" THEN GOTO 200
1000 GOTO 985
1005 CLS
1006 GOSUB 1100 ' SAVE FILE TO DISK
1007 COLOR 14: LOCATE 1, 3: PRINT "Enter 0 to quit to main menu"
1010 COLOR 2: LOCATE 3, 3: INPUT "Amend which record :"; NUM
1015 IF NUM > NUMBER THEN 1005
1020 IF NUM < 1 THEN GOTO 200
1025 COLOR 12: LOCATE 7, 2: PRINT QUESTION$
1030 COLOR 10: LOCATE 9, 2: PRINT " Q. "; X$(NUM)
1035 COLOR 12: LOCATE 11, 2: PRINT "ANSWER - "; : COLOR 10: PRINT Y$(NUM)
1040 COLOR 7: LOCATE 20, 5: PRINT "Change this record (Y)es or (N)o ?"
1045 A$ = INKEY$: IF A$ = "" THEN 1045
1050 IF A$ = "Y" OR A$ = "y" THEN 1065
1055 IF A$ = "N" OR A$ = "n" THEN GOTO 1005
1060 GOTO 1045
1065 LOCATE 20, 5: PRINT SPACE$(35);
1067 COLOR 15: LOCATE 9, 10: INPUT X$(NUM)
1070 LOCATE 10, 10: INPUT Y$(NUM)
1075 COLOR 7: LOCATE 20, 5: PRINT "Is this o.k. (Y)es or (N)o ?"
1080 A$ = INKEY$: IF A$ = "" THEN GOTO 1080
1085 IF A$ = "Y" OR A$ = "y" THEN GOTO 1005
1090 IF A$ = "N" OR A$ = "n" THEN GOTO 1065
1095 GOTO 1080
1099 REM ***** SAVE FILE TO DISK
1100 OPEN F$ + ".QIZ" FOR OUTPUT AS #1
1105 PRINT #1, QUESTION$
1110 PRINT #1, NUMBER
1115 FOR A = 1 TO NUMBER
1120 PRINT #1, X$(A)
1125 PRINT #1, Y$(A)
1130 NEXT A
1135 CLOSE
1140 RETURN

