10 REM HOUR OF THE BEASTIES
20 REM Written by A.Sill
30 REM In GWBASIC
40 REM (c) 1992
50 REM
60 DIM BEASTX%(100), BEASTY%(100), CHEESEX%(100), CHEESEY%(100)
70 DIM HI$(15), HI%(15)
80 SCREEN 9: CLS
90 A$ = "HOUR OF THE BEASTIES"
100 COLOR 2
110 LOCATE 2, (80 - LEN(A$)) / 2: PRINT A$
120 COLOR 10
130 COLOR 10: LOCATE 5, 1: PRINT "Beasties is a game where you are surrounded by a large number of wild, heathen, foul-smelling, dung-eating beasties that are constantly chasing their food whichtoday happens to be you."
140 PRINT "Fortunately if you can get to your matter-transporter you can escape their      vicious clutches and live another day.   One thing they are vulnerable to       is cheese and you must use this to your advantage and try to make them crash"
150 PRINT "accidentally into it as they chase you.   Contact with it kills them."
160 RAND = 0
170 LOCATE 20, 33: COLOR 12: PRINT "Press any key"
180 A$ = ""
190 A$ = INKEY$: RAND = RAND + .1: IF A$ = "" THEN 190
200 RAND = RAND - VAL(RIGHT$(TIME$, 2))
210 RANDOMIZE RAND
220 EXPBEASTS% = 10: EXPCHEESE% = 15: GAME% = 0: BEASTS% = 0: CHEESE% = 0: HITS% = 0: RUNONCE% = 0
230 GOTO 2210
240 REM ###### INTERFACE ######
250 CLS
260 FOR A = 1 TO 80
270 COLOR 14: LOCATE 1, A: PRINT "Ы"; :
280 LOCATE 2, A: PRINT "І"; :
290 LOCATE 3, A: PRINT "±"; :
300 LOCATE 4, A: PRINT "°"; :
310 LOCATE 25, A: PRINT "Ы";
320 LOCATE 24, A: PRINT "І";
330 LOCATE 23, A: PRINT "±";
340 LOCATE 22, A: PRINT "°";
350 NEXT A
360 COLOR 15: LOCATE 6, 28: PRINT "Hour of The BEASTIES"
370 COLOR 12
380 LOCATE 9, 25: PRINT "P ___________ Play new Game"
390 LOCATE 10, 25: PRINT "E ___________ Experiment"
400 LOCATE 11, 25: PRINT "H ___________ Hi-Score Table"
410 LOCATE 12, 25: PRINT "X ___________ Exit to DOS"
420 A$ = INKEY$: IF A$ = "" THEN GOTO 420
430 IF A$ = "P" OR A$ = "p" THEN GAME% = 1: CHEESE% = 20: BEASTS% = 3: GOTO 640
440 IF A$ = "E" OR A$ = "e" THEN GOTO 480
450 IF A$ = "H" OR A$ = "h" THEN GOTO 2210
460 IF A$ = "X" OR A$ = "x" THEN STOP
470 GOTO 420
480 REM ###### EXPERIMENT SCREEN ######
490 LOCATE 10, 55: PRINT "B   Increase Beasts"
500 LOCATE 11, 55: PRINT "D   Decrease Beasts"
510 LOCATE 12, 55: PRINT "C   Increase Cheese"
520 LOCATE 13, 55: PRINT "R   Decrease Cheese"
530 LOCATE 14, 55: PRINT "P   Play experiment"
540 LOCATE 15, 55: PRINT "M   Go Back To Menu"
550 LOCATE 20, 20: PRINT EXPBEASTS%; " BEASTS and "; EXPCHEESE%; " CHEESE.     "
560 A$ = INKEY$: IF A$ = "" THEN GOTO 560
570 IF A$ = "M" OR A$ = "m" THEN FOR A = 10 TO 15: LOCATE A, 55: PRINT STRING$(19, " "): NEXT A: LOCATE 20, 20: PRINT STRING$(60, " "): GOTO 420
580 IF (A$ = "B" OR A$ = "b") AND EXPBEASTS% < 100 THEN EXPBEASTS% = EXPBEASTS% + 1
590 IF (A$ = "C" OR A$ = "c") AND EXPCHEESE% < 100 THEN EXPCHEESE% = EXPCHEESE% + 1
600 IF (A$ = "R" OR A$ = "r") AND EXPCHEESE% > 3 THEN EXPCHEESE% = EXPCHEESE% - 1
610 IF (A$ = "D" OR A$ = "d") AND EXPBEASTS% > 1 THEN EXPBEASTS% = EXPBEASTS% - 1
620 IF A$ = "P" OR A$ = "p" THEN BEASTS% = EXPBEASTS%: CHEESE% = EXPCHEESE%: GOTO 640
630 GOTO 550
640 REM ###### INITIALISE GAME ######
650 WHILE INKEY$ <> "": WEND
660 IF RUNONCE% = 0 THEN GOSUB 2790
670 COLOR 15: LOCATE 22, 25: PRINT "PLEASE WAIT SETTING UP THE GAME"
680 X% = INT(RND * 77) + 2: Y% = INT(RND * 22) + 2: HITS% = 0
690 REM ### INITIALISE BEASTS' POSITIONS
700 FOR A = 1 TO BEASTS%
710 BEASTX%(A) = INT(RND * 77) + 2
720 BEASTY%(A) = INT(RND * 22) + 2
730 IF BEASTX%(A) = X% AND BEASTY%(A) = Y% THEN GOTO 710
740 NEXT A
750 REM ### INITIALISE CHEESE POSITIONS
760 FOR A = 1 TO CHEESE%
770 CHEESEX%(A) = INT(RND * 77) + 2
780 CHEESEY%(A) = INT(RND * 22) + 2
790 FLAG% = 0
800 REM CHECK FOR CHEESE/BEAST INITIALISE COLLISION
810 FOR B = 1 TO BEASTS%
820 IF CHEESEX%(A) = BEASTX%(B) AND CHEESEY%(A) = BEASTY%(B) THEN FLAG% = 1
830 NEXT B
840 IF FLAG% = 1 THEN GOTO 770
850 NEXT A
860 REM ### INITIALISE MATTER TRANSPORTER POSITION
870 MX% = INT(RND * 50) + 15: MY% = INT(RND * 15) + 5
880 IF MX% = X% AND MY% = Y% THEN GOTO 870
890 FLAG% = 0
900 FOR A = 1 TO BEASTS%
910 IF MX% = BEASTX%(A) AND MY% = BEASTY%(A) THEN FLAG% = 1
920 NEXT A
930 IF FLAG% = 1 THEN GOTO 870
940 FOR A = 1 TO CHEESE%
950 IF MX% = CHEESEX%(A) AND MY% = CHEESEY%(A) THEN FLAG% = 1
960 NEXT A
970 IF FLAG% = 1 THEN GOTO 870
980 REM ###### PRINT DETAILS ON SCREEN ######
990 LOCATE 22, 15: PRINT STRING$(60, " ");
1000 COLOR 15: LOCATE 22, 35: PRINT "PRESS A KEY"
1010 WHILE INKEY$ = "": WEND
1020 CLS
1030 FOR A = 1 TO 80
1040 COLOR 12: LOCATE 1, A: PRINT "Ы"
1050 LOCATE 25, A: PRINT "Ы";
1060 NEXT A
1070 FOR A = 1 TO 24
1080 LOCATE A, 1: PRINT "Ы";
1090 LOCATE A, 80: PRINT "Ы";
1100 NEXT A
1110 LOCATE 5, 5
1120 FOR A = 1 TO BEASTS%
1130 LOCATE BEASTY%(A), BEASTX%(A)
1140 COLOR 5: PRINT "ќ";
1150 NEXT A
1160 FOR A = 1 TO CHEESE%
1170 LOCATE CHEESEY%(A), CHEESEX%(A)
1180 COLOR 14: PRINT "O";
1190 NEXT A
1200 COLOR 2: LOCATE MY%, MX%: PRINT "±";
1210 COLOR 15: LOCATE Y%, X%: PRINT "";
1220 REM ###### GET READY ######
1230 LOCATE 1, 30: PRINT "GET READY : PRESS A KEY"
1240 WHILE INKEY$ = "": WEND
1250 LOCATE 1, 30: COLOR 12: PRINT "ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ"
1260 REM ###### MAIN GAME ######
1270 REM ### GET INPUT
1280 A$ = ""
1290 WHILE A$ = ""
1300 A$ = INKEY$
1310 WEND
1320 REM ### MOVE IN MEMORY
1330 DX% = X%: DY% = Y%
1340 IF A$ = CHR$(0) + "O" THEN X% = X% - 1: Y% = Y% + 1: GOTO 1430
1350 IF A$ = CHR$(0) + "P" THEN Y% = Y% + 1: GOTO 1430
1360 IF A$ = CHR$(0) + "Q" THEN X% = X% + 1: Y% = Y% + 1: GOTO 1430
1370 IF A$ = CHR$(0) + "K" THEN X% = X% - 1: GOTO 1430
1380 IF A$ = CHR$(0) + "M" THEN X% = X% + 1: GOTO 1430
1390 IF A$ = CHR$(0) + "G" THEN X% = X% - 1: Y% = Y% - 1: GOTO 1430
1400 IF A$ = CHR$(0) + "H" THEN Y% = Y% - 1: GOTO 1430
1410 IF A$ = CHR$(0) + "I" THEN X% = X% + 1: Y% = Y% - 1: GOTO 1430
1420 GOTO 1280
1430 REM ### CHECK THE MOVE FOR COLLISIONS
1440 FLAG% = 0
1450 IF X% < 2 OR X% > 79 OR Y% < 2 OR Y% > 24 THEN FLAG% = 1
1460 IF FLAG% = 1 THEN X% = DX%: Y% = DY%: GOTO 1270
1470 IF X% = MX% AND Y% = MY% THEN LOCATE DY%, DX%: PRINT " "; : GOTO 1800
1480 FLAG% = 0
1490 FOR A = 1 TO BEASTS%
1500 IF X% = BEASTX%(A) AND Y% = BEASTY%(A) THEN FLAG% = 1
1510 NEXT A
1520 IF FLAG% = 1 THEN GOTO 1990
1530 FLAG% = 0
1540 FOR A = 1 TO CHEESE%
1550 IF X% = CHEESEX%(A) AND Y% = CHEESEY%(A) THEN X% = DX%: Y% = DY%
1560 NEXT A
1570 REM ### MOVE YOU
1580 LOCATE DY%, DX%: PRINT " ";
1590 COLOR 15: LOCATE Y%, X%: PRINT "";
1600 REM ### MOVE BEASTIES
1610 FOR A = 1 TO BEASTS%
1620 IF BEASTX%(A) = 0 AND BEASTY%(A) = 0 THEN GOTO 1780
1630 DBEASTX% = BEASTX%(A): DBEASTY% = BEASTY%(A)
1640 IF X% > BEASTX%(A) THEN BEASTX%(A) = BEASTX%(A) + 1
1650 IF X% < BEASTX%(A) THEN BEASTX%(A) = BEASTX%(A) - 1
1660 IF Y% > BEASTY%(A) THEN BEASTY%(A) = BEASTY%(A) + 1
1670 IF Y% < BEASTY%(A) THEN BEASTY%(A) = BEASTY%(A) - 1
1680 FLAG% = 0
1690 FOR B = 1 TO CHEESE%
1700 IF BEASTX%(A) = CHEESEX%(B) AND BEASTY%(A) = CHEESEY%(B) THEN BEASTX%(A) = 0: BEASTY%(A) = 0: LOCATE DBEASTY%, DBEASTX%: PRINT " "; : FLAG% = 1
1710 IF BEASTX%(A) = MX% AND BEASTY%(A) = MY% THEN BEASTX%(A) = 0: BEASTY%(A) = 0: LOCATE DBEASTY%, DBEASTX%: PRINT " "; : FLAG% = 1
1720 NEXT B
1730 IF FLAG% = 1 THEN FOR A = 650 TO 635 STEP -3: SOUND A, 1: NEXT A: FOR A = 500 TO 100 STEP -50: SOUND A, .1: NEXT A: HITS% = HITS% + 1: GOTO 1780
1740 LOCATE BEASTY%(A), BEASTX%(A)
1750 COLOR 5: PRINT "ќ";
1760 LOCATE DBEASTY%, DBEASTX%: PRINT " ";
1770 IF BEASTX%(A) = X% AND BEASTY%(A) = Y% THEN GOTO 1990
1780 NEXT A
1790 GOTO 1270
1800 REM ###### CONGRATULATIONS : REACHED MATTER TRANSPORTER ######
1810 FOR A = 37 TO 100 STEP 2
1820 SOUND A, 1
1830 LOCATE Y%, X%: COLOR INT(RND * 13) + 1: PRINT "";
1840 NEXT A
1850 FOR A = 7 TO 18
1860 LOCATE A, 20: PRINT STRING$(40, "Ы")
1870 NEXT A
1880 FOR A = 8 TO 17
1890 LOCATE A, 22: PRINT STRING$(36, " ")
1900 NEXT A
1910 SCORE% = SCORE% + HITS% + 2
1920 COLOR 15: LOCATE 10, 26: PRINT "CONGRATULATIONS : YOU MADE IT"
1930 IF GAME% = 1 THEN COLOR 12: LOCATE 13, 26: PRINT "Score : "; SCORE%; "   Hits : "; HITS%
1940 BEASTS% = BEASTS% + 2
1950 FOR A = 1 TO 5000: NEXT A
1960 COLOR 7: LOCATE 15, 35: PRINT "Press a key"
1970 WHILE INKEY$ = "": WEND
1980 GOTO 640
1990 REM ###### DEAD ######
2000 FOR A = 100 TO 37 STEP -2
2010 SOUND A, 1
2020 IF A = 90 THEN LOCATE Y%, X%: COLOR 15: PRINT "R.";
2030 IF A = 72 THEN PRINT "I.";
2040 IF A = 54 THEN PRINT "P.";
2050 NEXT A
2060 FOR A = 7 TO 18
2070 LOCATE A, 20: PRINT STRING$(40, "Ы")
2080 NEXT A
2090 FOR A = 8 TO 17
2100 LOCATE A, 22: PRINT STRING$(36, " ")
2110 NEXT A
2120 COLOR 15: LOCATE 10, 29: PRINT "YOU HAVE BEEN EATEN BY"
2130 SCORE% = SCORE% + HITS%
2140 IF GAME% = 1 THEN COLOR 12: LOCATE 13, 35: PRINT "SCORE : "; SCORE%
2150 FOR A = 1 TO 200: COLOR INT(RND * 14) + 1: LOCATE 11, 34: PRINT "The BEASTIES": NEXT A
2160 COLOR 7: LOCATE 15, 35: PRINT "Press a key"
2170 WHILE INKEY$ = "": WEND
2180 IF GAME% = 1 THEN GOSUB 2480
2190 SCORE% = 0
2200 GOTO 240
2210 REM ###### SHOW HIGH SCORE TABLE ######
2220 CLS
2230 LOCATE 1, 1: COLOR 1: PRINT STRING$(80, "Ы");
2240 LOCATE 25, 1: PRINT STRING$(80, "Ы");
2250 FOR A = 2 TO 24
2260 LOCATE A, 1: PRINT "Ы";
2270 LOCATE A, 80: PRINT "Ы";
2280 NEXT A
2290 LOCATE 2, 2: COLOR 9: PRINT STRING$(78, "Ы");
2300 LOCATE 24, 2: PRINT STRING$(78, "Ы");
2310 FOR A = 2 TO 24
2320 LOCATE A, 2: PRINT "Ы";
2330 LOCATE A, 79: PRINT "Ы";
2340 NEXT A
2350 LOCATE 4, 23: COLOR 15: PRINT "HOUR OF THE BEASTIES HI-SCORE TABLE"
2360 OPEN "BEAST_HS" FOR INPUT AS #1
2370 FOR A = 1 TO 15
2380 INPUT #1, HI$(A)
2390 INPUT #1, HI%(A)
2400 LOCATE 5 + A, 12: PRINT STRING$(55, "_");
2410 LOCATE 5 + A, 12: PRINT A
2420 LOCATE 5 + A, 18: PRINT " "; HI$(A)
2430 LOCATE 5 + A, 63: PRINT HI%(A)
2440 NEXT A
2450 CLOSE #1
2460 WHILE INKEY$ = "": WEND
2470 GOTO 240
2480 REM ###### UPDATE HI-SCORE TABLE ######
VAR% = 0
FOR A = 15 TO 1 STEP -1
IF SCORE% > HI%(A) THEN VAR% = A
NEXT A
IF VAR% = 0 THEN GOTO 2780
CLS
LOCATE 1, 1: COLOR 9: PRINT STRING$(80, "Ы");
LOCATE 25, 1: PRINT STRING$(80, "Ы");
FOR A = 2 TO 24
LOCATE A, 1: PRINT "Ы";
LOCATE A, 80: PRINT "Ы";
NEXT A
COLOR 14: LOCATE 3, 15: PRINT "CONGRATULATIONS YOUR EFFORTS HAVE BEEN REWARDED"
COLOR 7: LOCATE 6, 5: PRINT "Your name must now be engraved upon the high score trophy for 'The Hour"
LOCATE 7, 5: PRINT "Of The Beasties'.   Please enter how you wish it to be represented : "
 FOR A = 14 TO VAR% STEP -1
HI$(A + 1) = HI$(A)
HI%(A + 1) = HI%(A)
NEXT A
HI%(VAR%) = SCORE%
COLOR 15: LOCATE 10, 15: INPUT "Enter your name : "; HISIG$
IF LEN(HISIG$) > 25 THEN HISIG$ = LEFT$(HISIG$, 25)
HI$(VAR%) = HISIG$
OPEN "BEAST_HS" FOR OUTPUT AS #1
FOR A = 1 TO 15
WRITE #1, HI$(A)
WRITE #1, HI%(A)
NEXT A
CLOSE #1
2780 RETURN

2790 REM ###### KEY SCREEN ######
2800 CLS
2810 LOCATE 1, 1: COLOR 2: PRINT STRING$(80, "Ы");
2820 LOCATE 25, 1: PRINT STRING$(80, "Ы");
2830 FOR A = 2 TO 24
2840 LOCATE A, 1: PRINT "Ы";
2850 LOCATE A, 80: PRINT "Ы";
2860 NEXT A
2870 LOCATE 2, 2: COLOR 10: PRINT STRING$(78, "±");
2880 LOCATE 24, 2: PRINT STRING$(78, "±");
2890 FOR A = 2 TO 24
2900 LOCATE A, 2: PRINT "±";
2910 LOCATE A, 79: PRINT "±";
2920 NEXT A
2930 LOCATE 4, 36: COLOR 14: PRINT "YOUR KEYS"
2940 LOCATE 7, 10: COLOR 7: PRINT "Please turn NUMSLOCK OFF"
2950 LOCATE 10, 15: PRINT "Use the Keypad as follows : "
2960 LOCATE 12, 35: COLOR 15: PRINT "7"; : COLOR 7: PRINT "|"; : COLOR 15: PRINT "8"; : COLOR 7: PRINT "|"; : COLOR 15: PRINT "9"
2970 LOCATE 13, 35: COLOR 15: PRINT "4"; : COLOR 7: PRINT "|"; : COLOR 15: PRINT "5"; : COLOR 7: PRINT "|"; : COLOR 15: PRINT "6"
2980 LOCATE 14, 35: COLOR 15: PRINT "1"; : COLOR 7: PRINT "|"; : COLOR 15: PRINT "2"; : COLOR 7: PRINT "|"; : COLOR 15: PRINT "3"
2990 LOCATE 16, 15: COLOR 7: PRINT "Use the above keys as directions.  Key 5 does nothing"
3000 RUNONCE% = 1
3010 RETURN

