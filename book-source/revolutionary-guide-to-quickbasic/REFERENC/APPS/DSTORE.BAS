10 REM DATASTORE
20 REM Written by A.Sill
30 REM FOR WROX PRESS
40 REM (c) 1992
50 REM
60 DIM FIELDNAME$(12), REC$(500, 12), FLAG1(100), FLAG2(100), U(500), NOA$(12)
70 BASE$ = "": RECORDS% = 0
80 CLS
85 PATH$ = ""           ' Enter path for files to be found
90 COLOR 15: LOCATE 2, 20: PRINT "DATASTORE - THE FLEXIBLE BASIC DATABASE"
100 COLOR 12: LOCATE 5, 5: PRINT "Current database : "; : COLOR 11: IF BASE$ = "" THEN PRINT "none";  ELSE PRINT BASE$
110 COLOR 12: LOCATE 5, 55: PRINT "Number of records : "; : COLOR 11: PRINT RECORDS%;
120 COLOR 10: LOCATE 8, 20: PRINT "1.   Create a new database"
130 IF BASE$ = "" THEN COLOR 8 ELSE COLOR 10
140 LOCATE 9, 20: PRINT "2.   Insert data into the current database"
150 IF BASE$ = "" OR RECORDS% = 0 THEN COLOR 8 ELSE COLOR 10
160 LOCATE 10, 20: PRINT "3.   Browse and edit current database"
170 IF BASE$ = "" OR RECORDS% = 0 THEN COLOR 8 ELSE COLOR 10
180 LOCATE 11, 20: PRINT "4.   Search the current database for data"
190 IF BASE$ = "" OR FIELDS% = 0 THEN COLOR 8 ELSE COLOR 10
200 LOCATE 12, 20: PRINT "5.   Change database or field names"
210 IF BASE$ = "" OR RECORDS% = 0 THEN COLOR 8 ELSE COLOR 10
220 LOCATE 13, 20: PRINT "6.   Sort database"
230 COLOR 10
240 LOCATE 14, 20: PRINT "7.   Load a previous database"
250 IF BASE$ = "" THEN COLOR 8 ELSE COLOR 10
260 LOCATE 15, 20: PRINT "8.   Save current database"
270 COLOR 10
280 LOCATE 16, 20: PRINT "9.   Quit program"
290 COLOR 14: LOCATE 24, 15: PRINT "This shareware was produced by A.Sill. (c). 1992";
300 A$ = INKEY$: IF A$ = "" THEN 300
310 A = VAL(A$)
320 IF A > 0 AND A < 10 THEN 330 ELSE 300
330 IF BASE$ = "" AND (A = 2 OR A = 3 OR A = 4 OR A = 5 OR A = 6 OR A = 8) THEN A$ = "": GOTO 300
340 IF RECORDS% = 0 AND (A = 3 OR A = 4 OR A = 6) THEN A$ = "": GOTO 300
350 IF FIELDS% = 0 AND (A = 5 OR A = 8) THEN A$ = "": GOTO 300
360 ON A GOSUB 380, 570, 740, 1090, 2630, 1800, 2090, 2300, 2500
370 GOTO 80
380 REM +++++ CREATE A NEW DATABASE
390 A$ = "CREATE A NEW DATABASE": GOSUB 2540
400 X = 7: GOSUB 2810
410 COLOR 14: LOCATE 7, 5: INPUT "Enter the name of the database : "; BASE$
420 LOCATE 9, 5: INPUT "Enter the number of fields in the database (Max. 12) :", FIELDS%
430 FOR A = 1 TO FIELDS%: LOCATE 21, 5: PRINT SPACE$(70): LOCATE A + 10, 5: PRINT "Enter name of field number "; A; : INPUT FIELDNAME$(A)
440 LOCATE 21, 5: PRINT "Enter type of string (A)lphanumeric or (N)umeric ?"
450 A$ = INKEY$: IF A$ = "" THEN 450
460 IF A$ = "A" OR A$ = "a" THEN NOA$(A) = "A": GOTO 490
470 IF A$ = "N" OR A$ = "n" THEN NOA$(A) = "N": GOTO 490
480 GOTO 450
490 NEXT A
500 COLOR 10: LOCATE 23, 5: PRINT "(A)ccept above (D)iscard above or (M)enu"
510 A$ = INKEY$: IF A$ = "" THEN 510
520 IF A$ = "A" OR A$ = "a" THEN RETURN
530 IF A$ = "D" OR A$ = "d" THEN GOTO 380
540 IF A$ = "M" OR A$ = "m" THEN BASE$ = "": RECORDS% = 0: FIELDS% = 0: RETURN
550 GOTO 510
560 RETURN
570 REM +++++ INSERT DATA INTO THE CURRENT DATABASE
580 A$ = "ENTER DATA INTO THE CURRENT DATABASE": GOSUB 2540
590 RECORDS% = RECORDS% + 1
600 COLOR 14: LOCATE 7, 5: PRINT "Record : "; RECORDS%
610 FOR A = 1 TO FIELDS%
620 COLOR 6: LOCATE 8 + A, 5: PRINT FIELDNAME$(A); " : ";
630 IF FLAG = 0 THEN INPUT REC$(RECORDS%, A) ELSE INPUT A$
640 IF FLAG = 1 AND A$ = "" THEN 650 ELSE IF FLAG = 1 AND A$ <> "" THEN REC$(NUM, A) = A$
650 NEXT A
660 IF FLAG = 1 THEN GOTO 760
670 COLOR 10: LOCATE 23, 5: PRINT "(M)enu (D)iscard or RETURN to continue"
680 A$ = INKEY$: IF A$ = "" THEN 680
690 IF A$ = "M" OR A$ = "m" THEN RETURN
700 IF A$ = "D" OR A$ = "d" THEN GOTO 600
710 IF A$ = CHR$(13) THEN GOTO 580
720 GOTO 680
730 RETURN
740 REM +++++ BROWSE AND EDIT CURRENT DATABASE
750 NUM = 1
760 A$ = "BROWSE AND EDIT CURRENT DATABASE": GOSUB 2540
770 FLAG = 0
780 COLOR 14: LOCATE 7, 5: PRINT "Record : "; NUM;
790 FOR A = 1 TO FIELDS%
800 COLOR 6: LOCATE 8 + A, 5: PRINT FIELDNAME$(A); " :   "; REC$(NUM, A); SPACE$(30)
810 NEXT A
820 COLOR 10: LOCATE 23, 1: PRINT "(M)enu (N)ext record (L)ast record (E)dit record (D)elete record (P)rint record (G)o to a record";
830 A$ = INKEY$: IF A$ = "" THEN 830
840 IF A$ = "M" OR A$ = "m" THEN RETURN
850 IF (A$ = "N" OR A$ = "n") AND NUM <> RECORDS% THEN NUM = NUM + 1: GOTO 770
860 IF (A$ = "L" OR A$ = "l") AND NUM <> 1 THEN NUM = NUM - 1: GOTO 770
870 IF A$ = "E" OR A$ = "e" THEN FLAG = 1: GOSUB 610
880 IF A$ = "P" OR A$ = "p" THEN LPRINT "Record Number "; NUM: FOR A = 1 TO FIELDS%: LPRINT FIELDNAME$(A); " "; REC$(NUM, A): NEXT A: GOTO 770
890 IF A$ = "D" OR A$ = "d" THEN GOTO 990
900 IF A$ = "G" OR A$ = "g" THEN GOTO 920
910 GOTO 830
920 REM GO TO A RECORD
930 LOCATE 23, 1: PRINT SPACE$(100);
940 LOCATE 22, 1: INPUT "Go to which record number :"; MOV%
950 IF INT(MOV%) < 1 OR INT(MOV%) > RECORDS% THEN GOTO 940
960 NUM = INT(MOV%)
970 LOCATE 22, 1: PRINT SPACE$(70);
980 GOTO 770
990 REM DELETE RECORD
1000 X = 20: GOSUB 2810
1010 IF ANSWER% = 1 THEN GOTO 1030
1020 IF ANSWER% = 0 THEN GOTO 760
1030 FOR A = NUM + 1 TO RECORDS%
1040 FOR B = 1 TO FIELDS%
1050 REC$(A - 1, B) = REC$(A, B)
1060 NEXT B, A
1070 RECORDS% = RECORDS% - 1: GOTO 760
1080 RETURN
1090 REM +++++ SEARCH THE CURRENT DATABASE FOR DATA
1100 C = 0
1110 FOR A = 1 TO 100: FLAG1(A) = 0: FLAG2(A) = 0: NEXT A
1120 A$ = "SEARCH THE CURRENT DATABASE FOR DATA": GOSUB 2540
1130 COLOR 14: LOCATE 7, 5: PRINT "Choose the search format :"
1140 COLOR 6: LOCATE 9, 10: PRINT "1. Find search data generally"
1150 LOCATE 10, 10: PRINT "2. FIELD NAME < search data"
1160 LOCATE 11, 10: PRINT "3. FIELD NAME > search data"
1170 LOCATE 12, 10: PRINT "4. Go back to main menu"
1180 A$ = INKEY$: IF A$ = "" THEN 1180
1190 IF VAL(A$) < 1 OR VAL(A$) > 4 THEN 1180
1200 COLOR 14: LOCATE 16, 10
1210 IF A$ = "4" THEN RETURN
1220 SM% = VAL(A$): ON SM% GOSUB 1240, 1330, 1430
1230 GOTO 1530
1240 REM =
1250 INPUT "Enter the data you wish to search for :"; SD$
1260 IF LEN(SD$) > 30 OR SD$ = "" THEN 1250
1270 FOR A = 1 TO RECORDS%
1280 FOR B = 1 TO FIELDS%
1290 IF NOA$(B) = "N" AND VAL(REC$(A, B)) = VAL(SD$) THEN C = C + 1: FLAG1(C) = A: FLAG2(C) = B
1300 IF NOA$(B) = "A" AND LEFT$(REC$(A, B), LEN(SD$)) = SD$ THEN C = C + 1: FLAG1(C) = A: FLAG2(C) = B
1310 NEXT B, A
1320 RETURN
1330 REM <
1340 INPUT "Enter FIELD NUMBER :"; F%
1350 IF F% < 1 OR F% > FIELDS% THEN 1200
1360 LOCATE 17, 10: INPUT "Enter the data :"; SD$
1370 IF SD$ = "" OR LEN(SD$) > 10 THEN 1360
1380 FOR A = 1 TO RECORDS%
1390 IF NOA$(F%) = "N" AND VAL(REC$(A, F%)) < VAL(SD$) THEN C = C + 1: FLAG1(C) = A: FLAG2(C) = F%
1400 IF NOA$(F%) = "A" AND LEFT$(REC$(A, F%), LEN(SD$)) < SD$ THEN C = C + 1: FLAG1(C) = A: FLAG2(C) = F%
1410 NEXT A
1420 RETURN
1430 REM >
1440 INPUT "Enter FIELD NUMBER :"; F%
1450 IF F% < 1 OR F% > FIELDS% THEN 1200
1460 LOCATE 17, 10: INPUT "Enter the data :"; SD$
1470 IF SD$ = "" OR LEN(SD$) > 10 THEN 1460
1480 FOR A = 1 TO RECORDS%
1490 IF NOA$(F%) = "N" AND VAL(REC$(A, F%)) > VAL(SD$) THEN C = C + 1: FLAG1(C) = A: FLAG2(C) = F%
1500 IF NOA$(F%) = "A" AND LEFT$(REC$(A, F%), LEN(SD$)) > SD$ THEN C = C + 1: FLAG1(C) = A: FLAG2(C) = F%
1510 NEXT A
1520 RETURN
1530 B = 1
1540 FOR A = 6 TO 20: LOCATE A, 1: PRINT SPACE$(80); : NEXT A
1550 IF C = 0 THEN COLOR 10: LOCATE 23, 5: PRINT "Sorry, no records found with this item.": WHILE INKEY$ = "": WEND: RETURN
1560 COLOR 10: LOCATE 21, 50: PRINT "Item found "; C; " times"
1570 LOCATE 7, 5: PRINT "Record : "; FLAG1(B); SPACE$(50)
1580 FOR A = 1 TO FIELDS%
1590 IF A = FLAG2(B) THEN COLOR 14 ELSE COLOR 6
1600 LOCATE 8 + A, 5: PRINT FIELDNAME$(A); " : "; REC$(FLAG1(B), A); SPACE$(30)
1610 NEXT A
1620 COLOR 10: LOCATE 23, 5: PRINT "(M)enu or re(S)earch (N)ext item found (L)ast item (P)rint record or print (A)ll";
1630 A$ = INKEY$: IF A$ = "" THEN 1630
1640 IF A$ = "M" OR A$ = "m" THEN RETURN
1650 IF A$ = "S" OR A$ = "s" THEN GOTO 1090
1660 IF (A$ = "N" OR A$ = "n") AND B <> C THEN B = B + 1: GOTO 1570
1670 IF (A$ = "L" OR A$ = "l") AND B <> 1 THEN B = B - 1: GOTO 1570
1680 IF A$ = "A" OR A$ = "a" THEN GOTO 1710
1690 GOTO 1630
1700 RETURN
1710 REM PRINT ALL FINDS
1720 LPRINT CHR$(27); "F"; BASE$; " Database": LPRINT "": LPRINT "Item found "; C; " times"
1730 FOR A = 1 TO C
1740 LPRINT "": LPRINT "Record Number "; FLAG1(A)
1750 FOR B = 1 TO FIELDS%
1760 IF B = FLAG2(A) THEN LPRINT CHR$(27); "E";  ELSE LPRINT CHR$(27); "F";
1770 LPRINT FIELDNAME$(B); " : "; REC$(FLAG1(A), B): NEXT B
1780 NEXT A
1790 GOTO 1630
1800 REM +++++ SORT DATABASE
1810 C = 32767
1820 FOR A = 1 TO RECORDS%: U(A) = 0: NEXT A
1830 A$ = "SORT CURRENT DATABASE INTO AN ORDER": GOSUB 2540
1840 X = 7: GOSUB 2810
1850 LOCATE 7, 5: PRINT "                                  "
1860 IF ANSWER% = 1 THEN GOTO 1880
1870 IF ANSWER% = 0 THEN RETURN
1880 FOR A = 1 TO FIELDS%: COLOR 14: LOCATE 6 + A, 5: PRINT A; " : "; FIELDNAME$(A): NEXT A
1890 COLOR 14: LOCATE 20, 5: PRINT "Sort database by which field ? ( 1 -"; FIELDS%; ")";
1900 INPUT F%
1910 IF F% < 1 OR F% > FIELDS% THEN 1900
1920 LOCATE 22, 10: PRINT "Please wait"
1930 IF NOA$(F%) = "A" THEN GOTO 1940 ELSE 2010
1940 REM ALPHABETICAL SORT
1950 FOR B% = 1 TO RECORDS%
1960 FOR A% = 2 TO RECORDS%
1970 IF REC$(A%, F%) < REC$(A% - 1, F%) THEN FOR C% = 1 TO FIELDS%: SWAP REC$(A%, C%), REC$(A% - 1, C%): NEXT C%
1980 NEXT A%, B%
1990 LOCATE 22, 10: PRINT SPACE$(11)
2000 COLOR 10: LOCATE 23, 7: PRINT "File has been sorted": WHILE INKEY$ = "": WEND: RETURN
2010 REM NUMERICAL SORT
2020 FOR B% = 1 TO RECORDS%
2030 FOR A% = 2 TO RECORDS%
2040 IF VAL(REC$(A%, F%)) < VAL(REC$(A% - 1, F%)) THEN FOR C% = 1 TO FIELDS%: SWAP REC$(A%, C%), REC$(A% - 1, C%): NEXT C%
2050 NEXT A%, B%
2060 LOCATE 21, 7: PRINT SPACE$(11)
2070 COLOR 10: LOCATE 23, 7: PRINT "File has been sorted": WHILE INKEY$ = "": WEND: RETURN
2080 RETURN
2090 REM +++++ LOAD A PREVIOUSLY SAVED DATABASE
2100 A$ = "LOAD A PREVIOUSLY SAVED DATABASE": GOSUB 2540
2110 IF BASE$ = "" THEN 2160 ELSE 2120
2120 COLOR 11: LOCATE 7, 5: PRINT "This will erase the current database."
2130 X = 9: GOSUB 2810
2140 IF ANSWER% = 1 THEN GOTO 2160
2150 IF ANSWER% = 0 THEN RETURN
2160 COLOR 14: LOCATE 9, 5: INPUT "Enter Path and Filename : "; FILENAME$
2170 OPEN FILENAME$ FOR INPUT AS #1
2180 INPUT #1, BASE$
2190 INPUT #1, FIELDS%
2200 INPUT #1, RECORDS%
2210 FOR A = 1 TO FIELDS%: INPUT #1, FIELDNAME$(A): INPUT #1, NOA$(A): NEXT A
2220 FOR A = 1 TO RECORDS%: FOR B = 1 TO FIELDS%
2230 INPUT #1, REC$(A, B)
2240 NEXT B, A
2250 CLOSE
2260 COLOR 10: LOCATE 13, 5: PRINT "The "; BASE$; " data base has been loaded."
2270 COLOR 6: LOCATE 20, 5: PRINT "Press any key to return to the menu"
2280 WHILE INKEY$ = "": WEND
2290 RETURN
2300 REM +++++ SAVE CURRENT DATABASE
2310 A$ = "SAVE THE CURRENT DATABASE TO DISK": GOSUB 2540
2320 X = 7: GOSUB 2810
2330 IF ANSWER% = 1 THEN GOTO 2350
2340 IF ANSWER% = 0 THEN RETURN
2350 COLOR 14: LOCATE 7, 5: INPUT "Enter Path and Filename : "; FILENAME$
2360 OPEN FILENAME$ FOR OUTPUT AS #1
2370 PRINT #1, BASE$
2380 PRINT #1, FIELDS%
2390 PRINT #1, RECORDS%
2400 FOR A = 1 TO FIELDS%: PRINT #1, FIELDNAME$(A): PRINT #1, NOA$(A): NEXT A
2410 FOR A = 1 TO RECORDS%: FOR B = 1 TO FIELDS%
2420 PRINT #1, REC$(A, B)
2430 NEXT B, A
2440 CLOSE
2450 COLOR 10: LOCATE 9, 5: PRINT "The "; BASE$; " data base has been saved."
2460 COLOR 6: LOCATE 11, 5: PRINT "Press any key to return to the menu"
2470 WHILE INKEY$ = "": WEND
2480 RETURN
2490 RETURN
2500 REM +++++ QUIT PROGRAM
2510 A$ = "QUIT DATASTORE": GOSUB 2540
2520 GOSUB 2870
2530 RETURN
2540 REM SCREEN HEADERS
2550 CLS
2560 COLOR 1: FOR A = 1 TO 80: PRINT "л"; : NEXT A
2570 FOR B = 1 TO 3
2580 PRINT "лл"; : COLOR 9: FOR A = 1 TO 76: PRINT "л"; : NEXT A: COLOR 1: PRINT "лл";
2590 NEXT B
2600 COLOR 1: FOR A = 1 TO 80: PRINT "л"; : NEXT A
2610 COLOR 15: LOCATE 3, (80 - LEN(A$)) / 2: PRINT A$
2620 RETURN
2630 REM +++++ CHANGE DATABASE OR FIELD NAMES
2640 A$ = "CHANGE DATABASE OR FIELD NAMES": GOSUB 2540
2650 COLOR 14: LOCATE 7, 5: PRINT "Do you wish to change the database name (Y)es or (N)o ?"
2660 A$ = INKEY$: IF A$ = "" THEN 2660
2670 IF A$ = "Y" OR A$ = "y" THEN LOCATE 9, 5: INPUT "Enter database name (8 characters max.) :"; BASE$: IF BASE$ = "" OR LEN(BASE$) > 8 THEN A$ = "Y": GOTO 2670 ELSE 2700
2680 IF A$ = "N" OR A$ = "n" THEN 2700
2690 GOTO 2660
2700 COLOR 14: LOCATE 11, 5: PRINT "Do you wish to change the field names (Y)es or (N)o ?   "
2710 A$ = INKEY$: IF A$ = "" THEN 2710
2720 IF A$ = "Y" OR A$ = "y" THEN 2750
2730 IF A$ = "N" OR A$ = "n" THEN RETURN
2740 GOTO 2710
2750 FOR A = 1 TO FIELDS%: COLOR 14: LOCATE 12 + A, 5: PRINT A: LOCATE A + 12, 8: PRINT FIELDNAME$(A): COLOR 6: LOCATE A + 12, 30
2760 INPUT A$: IF A$ <> "" THEN FIELDNAME$(A) = A$
2770 NEXT A
2780 PRINT : PRINT "     Press a key to return to the menu"
2790 WHILE INKEY$ = "": WEND
2800 RETURN
2810 REM YES OR NO INPUT ROUTINE
2820 LOCATE X, 5: COLOR 11: PRINT "Are you sure (Y)es or (N)o ?"
2830 A$ = INKEY$: IF A$ = "" THEN 2830
2840 IF A$ = "Y" OR A$ = "y" THEN ANSWER% = 1: RETURN
2850 IF A$ = "N" OR A$ = "n" THEN ANSWER% = 0: RETURN
2860 GOTO 2830
2870 REM ********* RETURN TO MENU POSER
2880 COLOR 12: PRINT : PRINT : PRINT : PRINT : PRINT "          Do you want to 1. Return to DATASTORE": PRINT "                         2. Return to Dos ?."
2890 LOCATE 8, 26: COLOR 7: PRINT "1"
2900 LOCATE 9, 26: PRINT "2"
2910 A$ = INKEY$: IF A$ = "" THEN GOTO 2910
2920 IF A$ = "1" THEN RETURN
2930 IF A$ = "2" THEN CLS : SYSTEM
2940 GOTO 2910

