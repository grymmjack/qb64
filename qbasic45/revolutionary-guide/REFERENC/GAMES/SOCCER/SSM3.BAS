10 REM Super Soccer Manager V3.0 Main Program
20 REM Written by A.SILL
30 REM (c). 1992
40 REM Part of THE BASIC ARRAY compilation of BASIC programs
50 I$="BUY"
60 IF T$(1)="" THEN CLS:PRINT "You must first run SSM1 to initialise Super Soccer Manager.":STOP
70 RANDOMIZE RAND%+VAL(RIGHT$(TIME$,2))
80 GOSUB 4000 ' SHOW DIVISION STARTING IN
90 IF LEAGUE%=1 THEN MONEY=1500000!
100 IF LEAGUE%=2 THEN MONEY=500000!
110 IF LEAGUE%=3 THEN MONEY=300000!
120 IF LEAGUE%=4 THEN MONEY=175000!
130 IF LEAGUE%=5 THEN MONEY=100000!
140 FOR A=1 TO 16:WINS%(A)=0:DRAWS%(A)=0:SCORE%(A)=0:LOSSES%(A)=0:POSITION%(A)=A:GFOR%(A)=0:GAGAINST%(A)=0:POINTS%(A)=0:GOALS%(A)=0:NEXT A
150 GOSUB 2710 ' SCRAMBLE WEEKLY FIXTURE ORDER
160 FOR A=1 TO 30:CHART%(A)=0:RES$(A)="":NEXT A
170 IF SEASON%>0 THEN GOSUB 2260 ' CORRECT TEAMS ORDER
180 GOSUB 2770 ' SCRAMBLE PLAYERS NAMES
190 IF SEASON%>0 THEN GOSUB 4000 ' SHOW DIVISION OF CHOSEN TEAM
200 IF SEASON%>0 THEN GOSUB 7760 ' GET NAMES FROM TEMP 2 FILE
210 REM ++++++++ OPENING SCREEN
220 B%=6-LEAGUE%:X=0
240 GOSUB 1960 ' CALCULATE FIXTURES
250 IF MONEY<(B%^2)*-20000 THEN GOSUB 6470
260 C=INT(RND*8)+1
270 FOR A=1 TO 8
272 IF A>=C THEN TRANSPL%(A)=0:TRANSTM%(A)=0:GOTO 300
275 BID%(A)=0
280 TRANSTM%(A)=INT(RND*15)+1:IF TRANSTM%(A)=TEAM% THEN GOTO 280
290 TRANSPL%(A)=INT(RND*15)+1:IF PLA$(TRANSTM%(A),TRANSPL%(A))="" THEN GOTO 290
295 FOR B=1 TO A-1
296 IF PLA$(TRANSTM%(B),TRANSPL%(B))=PLA$(TRANSTM%(A),TRANSPL%(A)) THEN GOTO 280
297 NEXT B
300 NEXT A
310 GOSUB 6040 ' SET TRANSFERS OFFER PRICES
320 WHILE X<>1
330 A$="SUPREME SOCCER MANAGER":SPECIAL%=1:GOSUB 6630
340 IF DISPLAY%=2 THEN COLOR 12 ELSE COLOR 15
350 LOCATE 6,3:PRINT TEAM$(TEAM%):LOCATE 6,63:PRINT "œ";MONEY:LOCATE 6,32:PRINT "WEEK :";WEEK%
360 A$="Part of THE BASIC ARRAY package":SPECIAL%=3:GOSUB 6630
365 COLOR 8:LOCATE 20,2:PRINT "Actual Date is ";DATE$
366 LOCATE 20,56:PRINT "Actual Time is ";TIME$
370 OPT%=6:CHOICE$(1)="Start Week":CHOICE$(2)="Start Again":CHOICE$(3)="Quit Super League Manager":CHOICE$(4)="Load game":CHOICE$(5)="Save game":CHOICE$(6)="See Club Status":GOSUB 2910
380 IF X>1 THEN ON X-1 GOSUB 3280,3350,2310,2510,7400
390 WEND
400 REM ++++++++ STATISTICS MENU
410 GOSUB 6040 ' SET TRANSFER PRICES
420 X=0
430 A$="STATISTICS MENU":GOSUB 6630
440 CHOICE$(1)="Continue":CHOICE$(2)="Show current league table":CHOICE$(3)="Show this week's fixtures"
450 IF DISPLAY%=2 THEN OPT%=7:CHOICE$(4)="Show progress chart":CHOICE$(5)="Show season's fixtures":CHOICE$(6)="Show top scorers":CHOICE$(7)="Go back to main menu"
460 IF DISPLAY%=1 THEN OPT%=6:CHOICE$(4)="Show season's fixtures":CHOICE$(5)="Show top scorers":CHOICE$(6)="Go back to main menu"
470 GOSUB 2910
480 IF (X=7 AND DISPLAY%=2) OR (X=6 AND DISPLAY%=1) THEN GOTO 320
490 IF DISPLAY%=2 THEN ON X GOSUB 520,1720,2060,4830,4980,4680
500 IF DISPLAY%=1 THEN ON X GOSUB 520,1720,2060,4980,4680
510 IF X<>1 THEN GOTO 430
520 REM ++++++++ TRANSFERS MENU
530 X=0
531 D=0:FOR A=1 TO 20:IF PLA$(TEAM%,A)<>"" THEN D=D+1
532 NEXT A
540 A$="TRANSFERS MENU":GOSUB 6630
550 OPT%=4:CHOICE$(1)="Enter Dressing Room":CHOICE$(2)="Sell a player":CHOICE$(3)="Examine the transfer market":CHOICE$(4)="Go back to the statistics menu":GOSUB 2910
560 IF X=2 AND I$<>"SELL" THEN GOSUB 7920:CHAIN MERGE "SELL",10000,ALL
565 IF X=2 AND I$="SELL" THEN GOTO 10000
567 IF X=3 AND D=20 THEN GOSUB 8165:GOTO 530
570 IF X=3 AND I$<>"BUY" THEN GOSUB 7920:CHAIN MERGE "BUY",10000,ALL
575 IF X=3 AND I$="BUY" THEN GOTO 10000
580 IF X=4 THEN GOTO 400
590 IF X<>1 THEN GOTO 530
600 GOSUB 4160 ' TEAM SELECTION SCREEN
610 REM ++++++++ GAME MENU
620 A$="GAME MENU":GOSUB 6630
630 OPT%=4:CHOICE$(1)="See Results only":CHOICE$(2)="See match summary":CHOICE$(3)="Watch teletext results service":CHOICE$(4)="Make changes to team":GOSUB 2910
640 IF X=1 THEN C=2:GOSUB 7920:GOTO 700
650 IF X=2 THEN GOTO 680
660 IF X=3 THEN C=3:GOTO 700
670 IF X=4 THEN GOTO 600
680 REM +++++++++ GAME SCREEN ++++++++
690 C=0
700 ' CHECK FOR 11 FIT PLAYERS
710 FOR A=1 TO 11:IF PLA$(TEAM%,A)="" THEN C=C+1
720 NEXT A
730 GOSUB 1960 ' CALCULATE FIXTURES
740 IF C=1 THEN GOSUB 7040
750 IF C=0 THEN A$="LATEST SCORE":GOSUB 6630
760 RAND%=RAND%-VAL(RIGHT$(TIME$,2))
770 L%=0:R%=0
780 RANDOMIZE RAND%
790 ' CALCULATE CHANCES
800 FOR A=1 TO 16:SCORE%(A)=0
810 CHANCES%(A)=0:FOR B=1 TO 11:CHANCES%(A)=CHANCES%(A)+(FIG%(A,B,1)/7)
820 NEXT B
830 IF FIG%(A,1,0)>1 THEN CHANCES%(A)=CHANCES%(A)*.7
840 IF A=TEAM% THEN CHANCES%(A)=CHANCES%(A)*.93
850 A$=FORMATION$:IF (A=TEAM%) AND (A$<>" 4 3 3" AND A$<>" 4 4 2" AND A$<>" 5 3 2" AND A$<>" 4 2 4" AND A$<>" 3 4 3" AND A$<>" 3 3 4" AND A$<>" 3 5 2" AND A$<>" 4 5 1" AND A$<>" 5 4 1") THEN CHANCES%(A)=CHANCES%(A)*.85
860 IF TEAM%=FIXT%(A) THEN B%=A
870 NEXT A
880 IF C=3 THEN A$="TELETEXT LATEST SCORES SERVICE":GOSUB 6630:GOSUB 8020
890 IF DISPLAY%=2 THEN COLOR 14 ELSE COLOR 15
900 IF C=0 THEN LOCATE 7,37:PRINT "0  :  0"
910 IF B%/2=INT(B%/2) THEN HOME%=FIXT%(B%-1):AWAY%=FIXT%(B%) ELSE HOME%=FIXT%(B%):AWAY%=FIXT%(B%+1)
920 IF C=1 OR C=2 THEN GOTO 7140
930 IF C<>3 THEN LOCATE 7,10:PRINT TEAM$(HOME%):LOCATE 7,55:PRINT TEAM$(AWAY%)
940 GATE%=((6-LEAGUE%)*1200) + ((17-POSITION%(HOME%))*((6-LEAGUE%)*200)) + (((17-POSITION%(AWAY%))*((6-LEAGUE%)*40)))*.9
950 GOSUB 6580 ' TEST FOR A KEY
960 LOCATE 23,1:PRINT SPACE$(80);
970 LOCATE 23,67:PRINT "GATE :";GATE%
980 IF C=3 THEN GOTO 1100
990 IF LEAGUE%=5 THEN A=CHANCES%(HOME%)-43:B=CHANCES%(AWAY%)-43
1000 IF LEAGUE%=4 THEN A=CHANCES%(HOME%)-61:B=CHANCES%(AWAY%)-61
1010 IF LEAGUE%=3 THEN A=CHANCES%(HOME%)-78:B=CHANCES%(AWAY%)-78
1020 IF LEAGUE%=2 THEN A=CHANCES%(HOME%)-98:B=CHANCES%(AWAY%)-98
1030 IF LEAGUE%=1 THEN A=CHANCES%(HOME%)-117:B=CHANCES%(AWAY%)-117
1040 IF A<2 THEN A=1 ELSE IF A>9 THEN A=10
1050 IF B<2 THEN B=1 ELSE IF B>9 THEN B=10
1060 LOCATE 23,38-LEN(GRADING$(A))
1070 PRINT GRADING$(A)
1080 LOCATE 23,41
1090 PRINT GRADING$(B)
1095 LOCATE 23,39:PRINT ":"
1100 LOCATE 23,2:PRINT "WEEK ";WEEK%
1120 ' BEGIN GAME LOOP
1130 FOR A=1 TO 90
1140 IF INKEY$<>"S" THEN FOR D=1 TO 1000:NEXT D
1150 IF DISPLAY%=2 THEN COLOR 7 ELSE COLOR 15
1160 LOCATE 5,36:PRINT "TIME :";A
1170 H=(DISPLAY%=2)
1180 IF A=45 AND C=0 THEN COLOR 15:LOCATE 5,36:PRINT "HALF TIME":B=0:WHILE INKEY$="" AND B<500:B=B+.1:WEND:LOCATE 5,36:PRINT "         ":COLOR 7+H:LOCATE 7,30:PRINT "(";SCORE%(HOME%);")":LOCATE 7,46:PRINT "(";SCORE%(AWAY%);")"
1190 IF A=45 AND C=3 THEN COLOR 15:LOCATE 5,36:PRINT "HALF TIME":B=0:WHILE INKEY$="" AND B<500:B=B+.1:WEND:FOR F=1 TO 8:LOCATE 5,36:PRINT "         ":COLOR 7+H:LOCATE 6+F,30:PRINT "(";SCORE%(FIXT%(F*2-1));")":LOCATE 6+F,46:PRINT "(";SCORE%(FIXT%(F*2));")"
1200 IF A=45 AND C=3 THEN NEXT F
1210 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
1220 T%=INT(RND*15)+1
1230 IF T%/2<>INT(T%/2) THEN B=T%+1
1240 IF T%/2=INT(T%/2) THEN T%=T%-1:B=T%+1
1260 D=INT(RND*5)
1270 IF D=2 THEN GOSUB 1280
1271 NEXT A
1272 GOTO 1360
1279 REM SCORE A GOAL
1280 FOR E=1 TO 16:CHANCE%(FIXT%(E))=CHANCES%(FIXT%(E))+INT(RND*25):NEXT E
1285 IF CHANCE%(FIXT%(T%))>CHANCE%(FIXT%(B)) THEN SCORE%(FIXT%(T%))=SCORE%(FIXT%(T%))+1:GFOR%(FIXT%(T%))=GFOR%(FIXT%(T%))+1:GAGAINST%(FIXT%(B))=GAGAINST%(FIXT%(B))+1:IF FIXT%(T%)=HOME% THEN TIMEH%(SCORE%(HOME%))=A
1290 IF CHANCE%(FIXT%(T%))<CHANCE%(FIXT%(B)) THEN SCORE%(FIXT%(B))=SCORE%(FIXT%(B))+1:GFOR%(FIXT%(B))=GFOR%(FIXT%(B))+1:GAGAINST%(FIXT%(T%))=GAGAINST%(FIXT%(T%))+1:IF FIXT%(B)=AWAY% THEN TIMEA%(SCORE%(AWAY%))=A
1300 IF C=3 THEN GOSUB 8020
1310 IF TIMEH%(L%+1)=A AND C=0 THEN L%=L%+1:LOCATE 7,36:GOSUB 3420:PRINT L%:LOCATE 8+L%,15:PRINT PLA$(HOME%,F);A:IF TEAM%=HOME% THEN GOALS%(F)=GOALS%(F)+1
1320 IF TIMEH%(L%+1)=A AND C=3 THEN L%=L%+1:GOSUB 3420:IF TEAM%=HOME% THEN GOALS%(F)=GOALS%(F)+1
1330 IF TIMEA%(R%+1)=A AND C=0 THEN R%=R%+1:LOCATE 7,42:GOSUB 3480:PRINT R%:LOCATE 8+R%,50:PRINT PLA$(AWAY%,F);A:IF AWAY%=TEAM% THEN GOALS%(F)=GOALS%(F)+1
1340 IF TIMEA%(R%+1)=A AND C=3 THEN R%=R%+1:GOSUB 3480:IF AWAY%=TEAM% THEN GOALS%(F)=GOALS%(F)+1
1350 RETURN
1360 COLOR 15:LOCATE 5,36:PRINT "FULL TIME"
1370 RES$(WEEK%)=STR$(L%)+" :"+STR$(R%)
1390 FOR A=1 TO 10:TIMEH%(A)=0:TIMEA%(A)=0:NEXT A
1400 T%=0:FOR A=1 TO 16:CHANCES%(A)=0:CHANCE%(A)=0:NEXT A
1410 GAMES%=GAMES%+1
1420 GOSUB 6580
1430 IF C<>3 THEN GOSUB 3090
1440 GOSUB 7920
1450 REM ++++++++ ASSIGN POINTS ++++++++
1460 FOR A=1 TO 8
1470 B=FIXT%(A*2-1):C=FIXT%(A*2)
1480 D=(RND*10)+1:E=(RND*10)+1
1490 IF SCORE%(B)>SCORE%(C) THEN POINTS%(B)=POINTS%(B)+3:WINS%(B)=WINS%(B)+1:LOSSES%(C)=LOSSES%(C)+1:FIG%(B,D,1)=FIG%(B,D,1)+2:FIG%(C,E,1)=FIG%(C,E,1)-2
1500 IF SCORE%(B)<SCORE%(C) THEN POINTS%(C)=POINTS%(C)+3:WINS%(C)=WINS%(C)+1:LOSSES%(B)=LOSSES%(B)+1:FIG%(B,D,1)=FIG%(B,D,1)-2:FIG%(C,E,1)=FIG%(C,E,1)+2
1510 IF SCORE%(B)=SCORE%(C) THEN POINTS%(C)=POINTS%(C)+1:POINTS%(B)=POINTS%(B)+1:DRAWS%(C)=DRAWS%(C)+1:DRAWS%(B)=DRAWS%(B)+1:FIG%(B,D,1)=FIG%(B,D,1)+RND*2-RND*2:FIG%(C,E,1)=FIG%(C,E,1)+RND*2-RND*2
1520 IF FIG%(C,E,1)>100-(LEAGUE%*12) THEN FIG%(C,E,1)=100-(LEAGUE%*12)
1530 IF FIG%(B,D,1)>100-(LEAGUE%*12) THEN FIG%(B,D,1)=100-(LEAGUE%*12)
1540 IF FIG%(B,D,1)<80-(LEAGUE%*12) THEN FIG%(B,D,1)=80-(LEAGUE%*12)
1550 IF FIG%(C,E,1)<80-(LEAGUE%*12) THEN FIG%(C,E,1)=80-(LEAGUE%*12)
1560 NEXT A
1570 REM ++++++++ SORT FOR NEW TABLE ++++++++
1580 FOR B=1 TO 16
1590 FOR A=1 TO 15
1600 C=POSITION%(A):D=POSITION%(A+1)
1610 IF POINTS%(C)<POINTS%(D) THEN E=D:POSITION%(A+1)=POSITION%(A):POSITION%(A)=E
1620 IF POINTS%(C)=POINTS%(D) AND (GFOR%(C)-GAGAINST%(C))<(GFOR%(D)-GAGAINST%(D)) THEN E=D:POSITION%(A+1)=POSITION%(A):POSITION%(A)=E
1630 IF POINTS%(C)=POINTS%(D) AND (GFOR%(C)-GAGAINST%(C))=(GFOR%(D)-GAGAINST%(D)) THEN IF GFOR%(C)<GFOR%(D) THEN E=D:POSITION%(A+1)=POSITION%(A):POSITION%(A)=E
1640 NEXT A,B
1650 FOR A=1 TO 16:IF POSITION%(A)=TEAM% THEN CHART%(WEEK%)=A
1660 NEXT A
1670 IF HOME%=TEAM% THEN POL=GATE%+INT(RND*1000)+INT(RND*1000):REC=GATE%*3*(RND+.6) ELSE POL=(B%)*500:REC=GATE%*(RND+.6)
1680 GOSUB 6160 ' SHOW FINANCIAL WEEK
1690 WEEK%=WEEK%+1
1700 IF WEEK%=31 THEN GOSUB 7920:CHAIN MERGE "END",10000,ALL
1710 GOTO 210
1720 REM ******** PRINT LEAGUE TABLE
1730 IF LEAGUE%>1 THEN A$="LEAGUE DIVISION "+STR$(LEAGUE%-1):GOSUB 6630 ELSE A$="PREMIER LEAGUE":GOSUB 6630
1740 IF DISPLAY%=2 THEN COLOR 2 ELSE COLOR 15
1750 LOCATE 5,42:PRINT "P   W   D   L   F   A  PTS"
1760 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
1770 FOR A=1 TO 16
1780 IF DISPLAY%=1 THEN GOTO 1810
1790 IF POSITION%(A)=TEAM% THEN COLOR 15 ELSE COLOR 10
1800 GOTO 1820
1810 IF POSITION%(A)=TEAM% THEN COLOR 7 ELSE COLOR 15
1820 B=5+A
1830 LOCATE B,6:PRINT A
1840 LOCATE 5+A,41:PRINT WINS%(POSITION%(A))+DRAWS%(POSITION%(A))+LOSSES%(POSITION%(A))
1850 LOCATE B,10:PRINT TEAM$(POSITION%(A))
1860 LOCATE B,45:PRINT WINS%(POSITION%(A))
1870 LOCATE B,49:PRINT DRAWS%(POSITION%(A))
1880 LOCATE B,53:PRINT LOSSES%(POSITION%(A))
1890 LOCATE B,57:PRINT GFOR%(POSITION%(A))
1900 LOCATE B,61:PRINT GAGAINST%(POSITION%(A))
1910 LOCATE B,65:PRINT POINTS%(POSITION%(A))
1920 NEXT A
1930 B=0:A=0:C=0:D=0:E=0
1940 GOSUB 6580
1950 RETURN
1960 REM ++++++++ CALCULATE FIXTURES
1970 FOR A=1 TO 8
1980 A$=MID$(FIXTURES$(WEEK%),(A*2)-1,1)
1990 IF ASC(A$)>64 THEN A$=STR$(ASC(A$)-55)
2000 B$=MID$(FIXTURES$(WEEK%),A*2,1)
2010 IF ASC(B$)>64 THEN B$=STR$(ASC(B$)-55)
2020 FIXT%(A*2-1)=VAL(A$)
2030 FIXT%(A*2)=VAL(B$)
2040 NEXT A
2050 RETURN
2060 REM ++++++++ SHOW FIXTURES
2070 A$="FIXTURES":GOSUB 6630
2080 IF DISPLAY%=2 THEN COLOR 3 ELSE COLOR 15
2090 LOCATE 5,34:PRINT "WEEK ";WEEK%
2100 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
2110 FOR A=1 TO 8
2120 A$=MID$(FIXTURES$(WEEK%),(A*2)-1,1)
2130 IF ASC(A$)>64 THEN A$=STR$(ASC(A$)-55)
2140 B$=MID$(FIXTURES$(WEEK%),A*2,1)
2150 IF ASC(B$)>64 THEN B$=STR$(ASC(B$)-55)
2160 IF VAL(A$)=TEAM% THEN COLOR 15 ELSE COLOR 10
2170 LOCATE 6+A,15:PRINT TEAM$(VAL(A$))
2180 IF VAL(B$)=TEAM% THEN COLOR 15 ELSE COLOR 10
2190 LOCATE 6+A,42:PRINT TEAM$(VAL(B$))
2200 COLOR 7
2210 LOCATE 6+A,38:PRINT "v"
2220 NEXT A
2230 GOSUB 6580
2240 RETURN
2250 REM ***** CORRECT TEAMS
2260 B=0:FOR A=1 TO 80:IF LEAGUE%(TEAMNO%)=LEAGUE%(A) THEN B=B+1:TEAM$(B)=T$(A):TN%(B)=A
2270 IF TEAMNO%=A THEN C=B:D=LEAGUE%(TEAMNO%)
2280 NEXT A
2290 TEAM%=C:LEAGUE%=D:FLAGX=1
2300 RETURN
2310 REM ++++++++ LOAD GAME
2320 A$="LOAD A SAVED GAME":GOSUB 6630
2330 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
2340 LOCATE 5,5:PRINT "Press <ENTER> or <RETURN> to abort."
2350 LOCATE 7,5:INPUT "Enter Filename :";A$
2360 IF LEN(A$)>8 THEN GOTO 2350
2370 IF A$="" THEN RETURN
2380 OPEN A$+".SAV" FOR INPUT AS #1
2390 INPUT #1,SEASON%:INPUT #1,TEAM%:INPUT #1,MONEY:INPUT #1,LEAGUE%:INPUT #1,FLAGX:INPUT #1,WEEK%:INPUT #1,TEAMNO%:INPUT #1,MANAGER$:INPUT #1,GROUND$:INPUT #1,GAMES%
2400 FOR A=1 TO 80:INPUT #1,T$(A):INPUT #1,LEAGUE%(A):NEXT A
2410 FOR A=1 TO 20:INPUT #1,GOALS%(A):INPUT #1,TN%(A):FOR B=1 TO 16:INPUT #1,PLA$(B,A):FOR C=0 TO 2:INPUT #1,FIG%(B,A,C):NEXT C,B,A
2420 FOR A=1 TO 16:INPUT #1,WINS%(A):INPUT #1,DRAWS%(A):INPUT #1,LOSSES%(A):INPUT #1,POSITION%(A):INPUT #1,GFOR%(A):INPUT #1,GAGAINST%(A):INPUT #1,POINTS%(A):INPUT #1,TEAM$(A):NEXT A
2430 FOR A=1 TO 30:INPUT #1,RES$(A):INPUT #1,CHART%(A):INPUT #1,FIXTURES$(A):NEXT A
2440 OPEN "NAMES.TMP" FOR OUTPUT AS #2
2450 FOR A=1 TO 1280
2460 INPUT #1,A$:PRINT #2,A$
2470 NEXT
2480 CLOSE #1:CLOSE #2
2490 GOSUB 6580
2500 RETURN
2510 REM ++++++++ SAVE GAME
2520 A$="SAVE THE CURRENT GAME":GOSUB 6630
2530 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
2540 LOCATE 5,5:PRINT "Press <ENTER> or <RETURN> to abort."
2550 LOCATE 7,5:INPUT "Enter Filename (Max. 8 Characters) :";A$
2560 IF LEN(A$)>8 THEN GOTO 2550
2570 IF A$="" THEN RETURN
2580 OPEN A$+".SAV" FOR OUTPUT AS #1
2590 PRINT #1,SEASON%:PRINT #1,TEAM%:PRINT #1,MONEY:PRINT #1,LEAGUE%:PRINT #1,FLAGX:PRINT #1,WEEK%:PRINT #1,TEAMNO%:PRINT #1,MANAGER$:PRINT #1,GROUND$:PRINT #1,GAMES%
2600 FOR A=1 TO 80:PRINT #1,T$(A):PRINT #1,LEAGUE%(A):NEXT
2610 FOR A=1 TO 20:PRINT #1,GOALS%(A):PRINT #1,TN%(A):FOR B=1 TO 16:PRINT #1,PLA$(B,A):FOR C=0 TO 2:PRINT #1,FIG%(B,A,C):NEXT C,B,A
2620 FOR A=1 TO 16:PRINT #1,WINS%(A):PRINT #1,DRAWS%(A):PRINT #1,LOSSES%(A):PRINT #1,POSITION%(A):PRINT #1,GFOR%(A):PRINT #1,GAGAINST%(A):PRINT #1,POINTS%(A):PRINT #1,TEAM$(A):NEXT
2630 FOR A=1 TO 30:PRINT #1,RES$(A):PRINT #1,CHART%(A):PRINT #1,FIXTURES$(A):NEXT
2640 OPEN "NAMES.TMP" FOR INPUT AS #2
2650 FOR A=1 TO 1280
2660 INPUT #2,A$:PRINT #1,A$
2670 NEXT
2680 CLOSE #1:CLOSE #2
2690 GOSUB 6580
2700 RETURN
2710 REM ++++++++ MIX UP FIXTURES
2720 FOR A=1 TO 15
2730 B=INT(RND*15)+1:C=INT(RND*15)+16
2740 SWAP FIXTURES$(B),FIXTURES$(C)
2750 NEXT
2760 RETURN
2770 REM ++++++++ SCRAMBLE PLAYERS
2780 FOR A=1 TO 16:FOR B=1 TO 20
2790 IF SEASON%=0 AND B>14 THEN GOTO 2890
2800 IF PLA$(A,B)="" THEN GOTO 2890
2810 E=(RND*20)+80-(LEAGUE%*12)
2820 F=(RND*18)+17
2830 FIG%(A,B,1)=E
2840 FIG%(A,B,2)=F
2850 IF B=1 THEN FIG%(A,B,0)=1
2860 IF (B>1 AND B<6) OR B=12 THEN FIG%(A,B,0)=2
2870 IF (B>5 AND B<10) OR B=13 THEN FIG%(A,B,0)=3
2880 IF (B>9 AND B<12) OR B=14 THEN FIG%(A,B,0)=4
2890 NEXT B,A
2900 RETURN
2910 REM ++++++++ MENU
2920 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
2930 FOR A=1 TO OPT%:LOCATE 7+A,27:PRINT CHOICE$(A):NEXT A
2940 IF DISPLAY%=2 THEN COLOR 15 ELSE COLOR 7
2950 LOCATE 8,25:PRINT "Û ";CHOICE$(1);" Û"
2960 X=1:DX=1
2970 A$=INKEY$:RAND=RAND-.2
2974 IF CHOICE$(4)="Load game" THEN COLOR 8:LOCATE 20,71:PRINT TIME$
2975 IF A$="" THEN GOTO 2970
2980 RANDOMIZE RAND
2990 IF A$=CHR$(13) THEN RETURN
3000 IF RIGHT$(A$,1)="H" THEN DX=X:X=X-1
3010 IF RIGHT$(A$,1)="P" THEN DX=X:X=X+1
3020 IF X=0 THEN X=OPT%
3030 IF X=OPT%+1 THEN X=1
3040 IF DISPLAY%=2 THEN COLOR 15 ELSE COLOR 7
3050 LOCATE 7+X,25:PRINT "Û ";CHOICE$(X);" Û"
3060 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
3070 LOCATE 7+DX,25:PRINT "  ";CHOICE$(DX);"  "
3080 GOTO 2970
3090 REM ++++++++ RESULTS SCREEN
3100 A$="LEAGUE RESULTS":GOSUB 6630
3110 IF DISPLAY%=2 THEN COLOR 3 ELSE COLOR 15
3120 LOCATE 5,37:PRINT "WEEK ";WEEK%
3130 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
3140 FOR A=1 TO 8
3150 A$=MID$(FIXTURES$(WEEK%),(A*2)-1,1)
3160 IF ASC(A$)>64 THEN A$=STR$(ASC(A$)-55)
3170 B$=MID$(FIXTURES$(WEEK%),A*2,1)
3180 IF ASC(B$)>64 THEN B$=STR$(ASC(B$)-55)
3190 IF VAL(A$)=TEAM% THEN COLOR 15 ELSE COLOR 10
3200 LOCATE 6+A,15:PRINT TEAM$(VAL(A$))
3210 IF VAL(B$)=TEAM% THEN COLOR 15 ELSE COLOR 10
3220 LOCATE 6+A,47:PRINT TEAM$(VAL(B$))
3230 IF DISPLAY%=2 THEN COLOR 7 ELSE COLOR 15
3240 LOCATE 6+A,36:PRINT SCORE%(FIXT%(A*2-1));" : ";SCORE%(FIXT%(A*2))
3250 NEXT
3260 GOSUB 6580
3270 RETURN
3280 REM ++++++++ START AGAIN
3290 A$="START AGAIN":GOSUB 6630
3300 COLOR 15:LOCATE 5,16:PRINT "Do you REALLY want to start again (Y)es or (N)o ?"
3310 A$=INKEY$:IF A$="" THEN 3310
3320 IF A$="Y" OR A$="y" THEN RUN "SSM1"
3330 IF A$="N" OR A$="n" THEN RETURN
3340 GOTO 3310
3350 REM ++++++++ QUIT SSM
3360 A$="QUIT SUPREME SOCCER MANAGER":GOSUB 6630
3370 COLOR 15:LOCATE 5,14:PRINT "Do you REALLY want to QUIT this program (Y)es or (N)o ?"
3380 A$=INKEY$:IF A$="" THEN 3380
3390 IF A$="Y" OR A$="y" THEN CLS:SYSTEM
3400 IF A$="N" OR A$="n" THEN RETURN
3410 GOTO 3380
3420 REM ++++++++ HOME SCORER
3430 H=0:F=0
3440 FOR D=2 TO 11:E=FIG%(HOME%,D,1)+RND*70+(FIG%(HOME%,D,0)*8)
3450 IF E>H AND PLA$(HOME%,D)<>"" THEN F=D:H=E
3460 NEXT D
3470 RETURN
3480 REM ++++++++ AWAY SCORER
3490 H=0:F=0
3500 FOR D=2 TO 11:E=FIG%(AWAY%,D,1)+RND*70+(FIG%(AWAY%,D,0)*8)
3510 IF E>H AND PLA$(AWAY%,D)<>"" THEN F=D:H=E
3520 NEXT D
3530 RETURN
4000 REM ++++++++ SHOW DIVISION YOUR STARTING IN
4010 A$="START OF SEASON":GOSUB 6630
4020 IF DISPLAY%=2 THEN GOTO 4050
4030 IF LEAGUE%=1 THEN COLOR 15 ELSE COLOR 7
4040 GOTO 4060
4050 IF LEAGUE%=1 THEN COLOR 15 ELSE COLOR 10
4060 LOCATE 6,33:PRINT "Premier League"
4070 FOR A=2 TO 5
4080 IF DISPLAY%=2 THEN GOTO 4110
4090 IF A=LEAGUE% THEN COLOR 15 ELSE COLOR 7
4100 GOTO 4120
4110 IF A=LEAGUE% THEN COLOR 15 ELSE COLOR 10
4120 LOCATE 6+(A-1)*2,35:PRINT "Division";A-1
4130 NEXT A
4140 GOSUB 6580
4145 GOSUB 7920
4150 RETURN
4160 REM ++++++++ TEAM SCREEN
4170 A$="TEAM SELECTION":GOSUB 6630
4180 B=0:C=0:D=0:FOR A=1 TO 11:IF FIG%(TEAM%,A,0)=2 THEN B=B+1 ELSE IF FIG%(TEAM%,A,0)=3 THEN C=C+1 ELSE IF FIG%(TEAM%,A,0)=4 THEN D=D+1
4190 NEXT A
4200 FORMATION$=STR$(B)+STR$(C)+STR$(D)
4210 IF DISPLAY%=2 THEN COLOR 11 ELSE COLOR 15
4220 COLOR 11:LOCATE 23,49:PRINT "Formation is ";B;"-";C;"-";D
4230 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
4240 COLOR 10:LOCATE 11,57:PRINT "Press RETURN or ENTER":LOCATE 12,58:PRINT "to play the game"
4250 COLOR 15:LOCATE 4,9:PRINT "NAME             POSITION   SKILL"
4260 X=1
4270 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
4280 LOCATE 6,58:PRINT "Use ARROW'S to move":LOCATE 7,60:PRINT "and press SPACE":LOCATE 8,56:PRINT "to select player to move"
4290 FOR A=1 TO 20
4300 IF A=X THEN COLOR 15:GOTO 4320
4310 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 7
4320 LOCATE A+4,1:PRINT A;
4330 IF PLA$(TEAM%,A)="" THEN LOCATE A+4,5:PRINT SPACE$(44);:GOTO 4370
4340 LOCATE A+4,5:PRINT PLA$(TEAM%,A)+SPACE$(10);
4350 LOCATE A+4,25:PRINT POSITION$(FIG%(TEAM%,A,0))+SPACE$(5);
4360 LOCATE A+4,37:PRINT FIG%(TEAM%,A,1);
4370 NEXT A
4380 C=0
4390 A$=INKEY$:IF A$="" THEN GOTO 4390
4400 IF A$=CHR$(13) THEN RETURN
4410 IF A$=" " THEN GOTO 4570
4420 IF RIGHT$(A$,1)="H" THEN B=X:X=X-1
4430 IF RIGHT$(A$,1)="P" THEN B=X:X=X+1
4440 IF X=0 THEN X=20
4450 IF X=21 THEN X=1
4460 COLOR 15:LOCATE X+4,1:PRINT X;
4470 IF PLA$(TEAM%,X)="" THEN GOTO 4490
4480 LOCATE X+4,5:PRINT PLA$(TEAM%,X);:LOCATE X+4,25:PRINT POSITION$(FIG%(TEAM%,X,0));:LOCATE X+4,37:PRINT FIG%(TEAM%,X,1);
4490 IF DISPLAY%=1 THEN GOTO 4520
4500 IF B=C THEN COLOR 4 ELSE COLOR 10
4510 GOTO 4530
4520 IF B=C THEN COLOR 15 ELSE COLOR 7
4530 LOCATE B+4,1:PRINT B;
4540 IF PLA$(TEAM%,B)="" THEN 4560
4550 LOCATE B+4,5:PRINT PLA$(TEAM%,B);:LOCATE B+4,25:PRINT POSITION$(FIG%(TEAM%,B,0));:LOCATE B+4,37:PRINT FIG%(TEAM%,B,1);
4560 GOTO 4390
4570 IF C<>0 THEN 4590
4580 C=X:GOTO 4390
4590 REM SWAP
4600 A$=PLA$(TEAM%,X):FOR A=0 TO 2:A%(A)=FIG%(TEAM%,X,A):NEXT A:D=GOALS%(X)
4610 PLA$(TEAM%,X)=PLA$(TEAM%,C):FOR A=0 TO 2:FIG%(TEAM%,X,A)=FIG%(TEAM%,C,A):NEXT A:GOALS%(X)=GOALS%(C)
4620 PLA$(TEAM%,C)=A$:FOR A=0 TO 2:FIG%(TEAM%,C,A)=A%(A):NEXT A:GOALS%(C)=D
4630 B=0:C=0:D=0:FOR A=1 TO 11:IF FIG%(TEAM%,A,0)=2 THEN B=B+1 ELSE IF FIG%(TEAM%,A,0)=3 THEN C=C+1 ELSE IF FIG%(TEAM%,A,0)=4 THEN D=D+1
4640 NEXT A
4650 IF DISPLAY%=2 THEN COLOR 11 ELSE COLOR 15
4660 LOCATE 23,49:PRINT "Formation is ";B;"-";C;"-";D
4670 C=0:GOTO 4290
4680 REM ++++++++ SHOW TOP SCORERS
4690 A$="TOP SCORERS":GOSUB 6630
4700 FOR A=0 TO 20:A%(A)=GOALS%(A):NEXT A
4710 FOR A=1 TO 16
4720 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
4730 LOCATE 4+A,30:PRINT A
4740 C=0:FOR B=1 TO 20:IF A%(B)>A%(C) THEN C=B
4750 NEXT B
4760 IF C=0 THEN 4800
4770 LOCATE 4+A,34:PRINT PLA$(TEAM%,C)
4780 LOCATE 4+A,52:PRINT A%(C)
4790 A%(C)=0
4800 NEXT A
4810 GOSUB 6580
4820 RETURN
4830 REM ++++++++ SHOW PROGRESS CHART
4840 A$="LEAGUE PROGRESS CHART":GOSUB 6630
4850 COLOR 15:LOCATE 6,11:PRINT "1"
4860 LOCATE 19,10:PRINT "16"
4870 LINE (94,256)-(560,256),7
4880 B=96:C=CHART%(1)*12+60
4890 LINE (92,256)-(92,72),7
4900 FOR A=2 TO 30
4910 IF CHART%(A)=0 THEN 4940
4920 LINE (B,C)-(80+A*16,CHART%(A)*12+60),15
4930 B=80+A*16:C=CHART%(A)*12+60
4940 NEXT A
4960 GOSUB 6580
4970 RETURN
4980 REM ++++++++ SHOW SEASONS FIXTURES/RESULTS
4990 A$="SEASONS FIXTURES/RESULTS":GOSUB 6630
5000 FOR A=1 TO 30
5010 C=0
5020 IF A=WEEK% THEN COLOR 15 ELSE COLOR 10
5030 FOR B=1 TO 16
5040 IF TEAM%<10 AND VAL(MID$(FIXTURES$(A),B,1))=TEAM% THEN C=B
5050 IF TEAM%>9 AND ASC(MID$(FIXTURES$(A),B,1))-55=TEAM% THEN C=B
5060 NEXT B
5070 IF A<16 THEN LOCATE 5+A,5 ELSE LOCATE 5+(A-15),40
5080 IF C/2<>INT(C/2) THEN PRINT "Home v ";:A$=MID$(FIXTURES$(A),C+1,1) ELSE PRINT "Away v ";:A$=MID$(FIXTURES$(A),C-1,1)
5090 IF ASC(A$)<65 THEN PRINT TEAM$(VAL(A$)) ELSE PRINT TEAM$(ASC(A$)-55)
5100 IF RES$(A)="" THEN GOTO 5140 ELSE IF A<16 AND C/2<>INT(C/2) THEN LOCATE 5+A,31 ELSE IF A<16 AND C/2=INT(C/2) THEN LOCATE 5+A,32 ELSE IF A>15 THEN LOCATE 5+(A-15),72
5110 IF A<16 THEN LOCATE 5+A,31 ELSE LOCATE 5+(A-15),71
5130 PRINT RES$(A);
5140 NEXT A
5150 GOSUB 6580
5160 RETURN
6040 REM SET OFFER PRICE
6050 FOR A=1 TO C
6060 IF LEAGUE%=5 THEN PRICE=(FIG%(TRANSTM%(A),TRANSPL%(A),1)-20)*4500+(RND*6000)-(RND*3000)
6070 IF LEAGUE%=4 THEN PRICE=(FIG%(TRANSTM%(A),TRANSPL%(A),1)-25)*5600+(RND*8000)-(RND*5000)
6080 IF LEAGUE%=3 THEN PRICE=(FIG%(TRANSTM%(A),TRANSPL%(A),1)-35)*8000+(RND*9500)-(RND*5000)
6090 IF LEAGUE%=2 THEN PRICE=(FIG%(TRANSTM%(A),TRANSPL%(A),1)-40)*15000+(RND*38000!)-(RND*25000)
6100 IF LEAGUE%=1 THEN PRICE=(FIG%(TRANSTM%(A),TRANSPL%(A),1)-60)*60000!+(RND*300000!)-(RND*265000!)
6110 IF PRICE>100000! THEN C=PRICE/100000!:D=INT(C):PRICE=D*100000! ELSE C=PRICE/1000:D=INT(C):PRICE=D*1000
6120 IF PRICE<1000 THEN PRICE=0
6130 PRICE(A)=PRICE:C=RND:IF C<.6 THEN 6130 ELSE BOTDOL(A)=PRICE*C*1.1
6140 NEXT A
6150 RETURN
6160 REM +++++ THE FINANCIAL WEEK
6170 B%=6-LEAGUE%:B=0:C=0
6180 D=B%*4000+((LEAGUE%=1)*1000):E=INT(B%*1000*B%*(RND+.5))
6190 C=(INT(RND*3)+1)*((B%^2)*1000)+((LEAGUE%=1)*-30000!):IF RND>.7 THEN C=C+8000
6200 FOR A=1 TO 16:B=B+(FIG%(TEAM%,A,1)*10)+(B%*125)+((LEAGUE%=1)*-7000):NEXT A
6210 IF FORFEIT%=1 THEN POL=0:GATE%=0:C=C*.75:FORFEIT%=0
6220 A$="THE FINANCIAL WEEK":GOSUB 6630
6230 COLOR 15:LOCATE 7,17:PRINT "INCOME                           EXPENDITURE"
6240 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
6250 LOCATE 9,5:PRINT "Gate receipts           ";REC
6260 LOCATE 10,5:PRINT "Transfer Sales          ";TIN
6270 LOCATE 11,5:PRINT "Advertising (TV etc.)   ";C
6280 LOCATE 12,5:PRINT "Sales                   ";E
6290 IF DISPLAY%=2 THEN COLOR 14 ELSE COLOR 15
6300 LOCATE 13,20:PRINT "TOTAL = œ";REC+TIN+C+E
6310 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
6320 LOCATE 9,43:PRINT "Transfer fee's          ";TOUT
6330 LOCATE 10,43:PRINT "Police costs            ";POL
6340 LOCATE 11,43:PRINT "Players wages           ";B
6350 LOCATE 12,43:PRINT "Extra staff wages       ";D
6360 IF DISPLAY%=2 THEN COLOR 14 ELSE COLOR 15
6370 LOCATE 13,58:PRINT "TOTAL = œ";TOUT+POL+B+D
6380 LOCATE 15,30:PRINT "Weeks Balance is œ";INT((REC+TIN+C+E)-(TOUT+POL+B+D))
6390 MONEY=INT(MONEY+REC+TIN+C+E-TOUT-POL-B-D)
6400 IF DISPLAY%=2 THEN COLOR 4 ELSE COLOR 15
6410 IF MONEY<0 THEN LOCATE 17,34:PRINT "BANK WARNING"
6420 COLOR 15
6430 LOCATE 19,28:PRINT "Bank Balance is œ";MONEY
6440 GOSUB 6580
6450 A=0:B=0:TIN=0:B=0:C=0:TOUT=0:POL=0:D=0:E=0:REC=0
6460 RETURN
6470 REM ++++++++ GAME OVER DEBT SCREEN
6480 A$="GAME OVER":SPECIAL%=1:GOSUB 6630
6490 IF DISPLAY%=2 THEN COLOR 2 ELSE COLOR 15
6500 LOCATE 8,5:PRINT "YOU HAVE LEAD  ";:PRINT TEAM$(TEAM%)
6510 LOCATE 10,5:COLOR 2:PRINT "INTO DEBT TOTALLING :"
6520 IF DISPLAY%=2 THEN COLOR 4 ELSE COLOR 15
6530 LOCATE 12,30:PRINT "œ";ABS(MONEY)
6540 IF DISPLAY%=2 THEN COLOR 2 ELSE COLOR 15
6550 LOCATE 14,5:PRINT "YOUR SERVICES ARE NO LONGER REQUIRED AT THIS CLUB."
6560 PRINT "    YOU ARE FROM THIS MOMENT IN TIME, SACKED AS MANAGER."
6570 GOSUB 6580:RUN "SSM1"
6580 REM ++++++++ TEST FOR A KEY
6590 A$="PRESS A KEY":SPECIAL%=3:GOSUB 6630
6600 WHILE INKEY$="":RAND=RAND+1:IF RAND=30000 THEN RAND=0 ELSE IF RAND=-30000 THEN RAND=0
6610 WEND
6620 RETURN
6630 REM ++++++++ CLEAR AND HEAD SCREEN
6640 IF SPECIAL%<2 AND DISPLAY%=2 THEN SCREEN 9:CLS
6650 IF SPECIAL%<2 AND DISPLAY%=1 THEN CLS
6660 IF DISPLAY%=2 THEN GOTO 6680
6670 IF SPECIAL%=1 THEN COLOR 15:FOR A=1 TO 80:LOCATE 1,A:PRINT "Ü";:LOCATE 5,A:PRINT "ß";:LOCATE 2,A:PRINT "Û";:LOCATE 3,A:PRINT "Û";:LOCATE 4,A:PRINT "Û";:NEXT A:LOCATE 3,(80-LEN(A$))/2:PRINT A$:GOTO 6690
6680 IF SPECIAL%=1 THEN FOR A=1 TO 80:COLOR 4:LOCATE 1,A:PRINT "Ü";:LOCATE 5,A:PRINT "ß";:COLOR 6:LOCATE 2,A:PRINT "Û";:LOCATE 3,A:PRINT "Û";:LOCATE 4,A:PRINT "Û";:NEXT A:COLOR 14:LOCATE 3,(80-LEN(A$))/2:PRINT A$
6690 IF DISPLAY%=2 THEN GOTO 6710
6700 IF SPECIAL%=0 THEN COLOR 15:FOR A=1 TO 80:LOCATE 1,A:PRINT CHR$(220);:LOCATE 3,A:PRINT CHR$(223);:LOCATE 2,A:PRINT CHR$(219);:NEXT A:LOCATE 2,(80-LEN(A$))/2:PRINT A$:GOTO 6720
6710 IF SPECIAL%=0 THEN FOR A=1 TO 80:COLOR 4:LOCATE 1,A:PRINT CHR$(220);:LOCATE 3,A:PRINT CHR$(223);:COLOR 6:LOCATE 2,A:PRINT CHR$(219);:NEXT A:COLOR 14:LOCATE 2,(80-LEN(A$))/2:PRINT A$
6720 IF DISPLAY%=2 THEN GOTO 6740
6730 IF SPECIAL%=3 THEN COLOR 15:FOR A=1 TO 80:LOCATE 22,A:PRINT "Ü";:LOCATE 24,A:PRINT "ß";:LOCATE 23,A:PRINT "Û";:NEXT A:LOCATE 23,(80-LEN(A$))/2:PRINT A$:GOTO 6750
6740 IF SPECIAL%=3 THEN FOR A=1 TO 80:COLOR 4:LOCATE 22,A:PRINT "Ü";:LOCATE 24,A:PRINT "ß";:COLOR 6:LOCATE 23,A:PRINT "Û";:NEXT A:COLOR 14:LOCATE 23,(80-LEN(A$))/2:PRINT A$
6750 SPECIAL%=0
6760 RETURN
6990 REM ******** RELEGATION FROM THE FOOTBALL LEAGUE
7000 A$="RELEGATION FROM THE FOOTBALL LEAGUE":SPECIAL%=1:GOSUB 6630
7010 COLOR 15:LOCATE 7,5:PRINT "You have been relegated from the Football League and are sacked as manager."
7020 GOSUB 6580
7030 RUN "PART1"
7040 REM ++++++++ FORFEIT MATCH SCREEN
7050 SPECIAL%=0:A$="MATCH FORFEITED":GOSUB 6630
7060 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
7070 LOCATE 7,5:PRINT "You do not have enough fit players to field a full team, thus to avoid a"
7080 PRINT "farce, you have to forfeit the match. A score of 5 - 0 is given against you, youwill also lose any gate receipts due to the cancellation. It is in your own bestinterests to stop these circumstances from occuring again otherwise a"
7090 PRINT "disqualification from the league could be a consequence."
7100 PRINT:PRINT "                        Yours faithfully"
7110 PRINT:PRINT "                                   Mr Adrian Sill                                                           (Chairman of the Super Soccer League)."
7120 GOSUB 6580
7130 RETURN
7140 REM ++++++++ CALCULATE SCORES
7150 IF C=2 THEN GATE%=((6-LEAGUE%)*1200) + ((17-POSITION%(HOME%))*((6-LEAGUE%)*200)) + (((17-POSITION%(AWAY%))*((6-LEAGUE%)*40)))*.9
7160 FOR A=1 TO 90
7170 T%=INT(RND*15)+1
7180 IF T%/2=INT(T%/2) THEN T%=T%-1:B=T%+1:GOTO 7200
7190 IF T%/2<>INT(T%/2) THEN B=T%+1
7200 GOAL%=0:D=INT(RND*5)
7210 IF C=2 THEN GOTO 7230
7220 IF FIXT%(T%)=HOME% OR FIXT%(T%)=AWAY% THEN GOTO 7280
7230 FOR E=1 TO 16:CHANCE%(FIXT%(E))=CHANCES%(FIXT%(E))+INT(RND*25):NEXT E
7240 IF D=2 AND CHANCE%(FIXT%(T%))>CHANCE%(FIXT%(B)) THEN GOAL%=1:SCORE%(FIXT%(T%))=SCORE%(FIXT%(T%))+1:GFOR%(FIXT%(T%))=GFOR%(FIXT%(T%))+1:GAGAINST%(FIXT%(B))=GAGAINST%(FIXT%(B))+1:IF FIXT%(T%)=HOME% THEN TIMEH%(SCORE%(HOME%))=A
7250 IF D=2 AND CHANCE%(FIXT%(T%))<CHANCE%(FIXT%(B)) THEN GOAL%=1:SCORE%(FIXT%(B))=SCORE%(FIXT%(B))+1:GFOR%(FIXT%(B))=GFOR%(FIXT%(B))+1:GAGAINST%(FIXT%(T%))=GAGAINST%(FIXT%(T%))+1:IF FIXT%(B)=AWAY% THEN TIMEA%(SCORE%(AWAY%))=A
7260 IF TIMEH%(L%+1)=A THEN L%=L%+1:GOSUB 3420:IF TEAM%=HOME% THEN GOALS%(F)=GOALS%(F)+1
7270 IF TIMEA%(R%+1)=A THEN R%=R%+1:GOSUB 3480:IF AWAY%=TEAM% THEN GOALS%(F)=GOALS%(F)+1
7280 NEXT A
7290 IF C=2 THEN RES$(WEEK%)="  "+STR$(L%)+" :"+STR$(R%):GOTO 7310
7300 IF HOME%=TEAM% THEN RES$(WEEK%)="0 : 5" ELSE RES$(WEEK%)="5 : 0"
7310 IF LEFT$(RES$(WEEK%),1)=" " THEN RES$(WEEK%)=RIGHT$(RES$(WEEK%),LEN(RES$(WEEK%))-1):GOTO 7310
7320 T%=0:FOR A=1 TO 16:CHANCE%(A)=0:CHANCES%(A)=0:NEXT A
7330 IF C=2 THEN GAMES%=GAMES%+1:GOTO 1430
7340 FOR A=1 TO 16
7350 IF FIXT%(A)=TEAM% AND TEAM%=HOME% THEN SCORE%(FIXT%(A))=0:SCORE%(FIXT%(A+1))=5 ELSE IF FIXT%(A)=TEAM% AND TEAM%=AWAY% THEN SCORE%(FIXT%(A))=0:SCORE%(FIXT%(A-1))=5
7360 IF FIXT%(A)=HOME% AND TEAM%=HOME% THEN GFOR%(AWAY%)=GFOR%(AWAY%)+5:GAGAINST%(HOME%)=GAGAINST(HOME%)+5 ELSE IF FIXT%(A)=TEAM% AND TEAM%=AWAY% THEN GFOR%(HOME%)=GFOR%(HOME%)+5:GAGAINST%(AWAY%)=GAGAINST%(AWAY%)+5
7370 NEXT A
7380 FORFEIT%=1
7390 GOTO 1430
7400 REM ++++++++ STATUS SCREEN
7410 SPECIAL%=0:A$="CLUB STATUS SCREEN":GOSUB 6630
7420 A$="Manager is "+MANAGER$
7430 COLOR 15:LOCATE 6,(80-LEN(A$))/2:PRINT A$
7440 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
7450 LOCATE 9,25:PRINT "Club               :  ";TEAM$(TEAM%)
7460 LOCATE 10,25:PRINT "Ground             :  ";LEFT$(GROUND$,19)
7470 LOCATE 11,25:PRINT "Club Founded       :";MID$(GROUND$,56,6)
7480 LOCATE 12,25:PRINT "Bank Balance       :  œ";MONEY
7490 LOCATE 13,25:PRINT "Position           : ";:FOR A=1 TO 16:IF POSITION%(A)=TEAM% THEN PRINT A
7500 NEXT A
7510 B=0:FOR A=1 TO 20:IF PLA$(TEAM%,A)<>"" THEN B=B+1
7520 NEXT A
7530 LOCATE 14,25:PRINT "Team Players       : ";B
7540 LOCATE 15,25:PRINT "Season's in charge : ";SEASON%
7550 LOCATE 16,25:PRINT "Games in charge    : ";GAMES%
7560 LOCATE 17,25:PRINT "Nickname           :";MID$(GROUND$,41,14)
7570 LOCATE 18,25:PRINT "Team Colours       :  ";MID$(GROUND$,21,19)
7580 GOSUB 6580
7590 RETURN
7760 REM ***** GET NAMES FOR NEW SEASON
7770 B=1:C=1
7780 OPEN "NAMES.TP2" FOR INPUT AS #1
7790 OPEN "NAMES.TMP" FOR OUTPUT AS #2
7800 WHILE B<81
7810 FOR A=1 TO 16
7820 INPUT #1,A$
7830 IF LEAGUE%(B)=LEAGUE% THEN PLA$(C,A)=A$
7840 PRINT #2,A$
7850 NEXT A
7860 IF LEAGUE%(B)=LEAGUE% THEN C=C+1
7870 B=B+1
7880 WEND
7890 CLOSE #1
7900 CLOSE #2
7910 RETURN
7920 REM ******** PLEASE WAIT SCREEN
7930 CLS
7940 IF DISPLAY%=1 THEN GOTO 8018
7945 A=RND*7+1
7990 LINE (100,20)-(540,50),A,BF
7991 LINE (104,23)-(536,47),0,BF
7992 LINE (100,20)-(540,50),15,B
8018 COLOR 15:LOCATE 3,35:PRINT "Please Wait"
8019 RETURN
8020 REM ++++++++ WATCH TELETEXT SERVICE
8030 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
8040 FOR G=1 TO 8
8050 A$=MID$(FIXTURES$(WEEK%),(G*2)-1,1)
8060 IF ASC(A$)>64 THEN A$=STR$(ASC(A$)-55)
8070 B$=MID$(FIXTURES$(WEEK%),G*2,1)
8080 IF ASC(B$)>64 THEN B$=STR$(ASC(B$)-55)
8090 IF VAL(A$)=TEAM% THEN COLOR 15 ELSE COLOR 10
8100 LOCATE 6+G,10:PRINT TEAM$(VAL(A$))
8110 IF VAL(B$)=TEAM% THEN COLOR 15 ELSE COLOR 10
8120 LOCATE 6+G,54:PRINT TEAM$(VAL(B$))
8130 IF DISPLAY%=2 THEN COLOR 7 ELSE COLOR 15
8140 LOCATE 6+G,36:PRINT SCORE%(FIXT%(G*2-1));" : ";SCORE%(FIXT%(G*2))
8150 NEXT G
8160 RETURN
8165 REM **** CAN'T BUY : TOO MANY PLAYERS
8170 SPECIAL%=0:A$="ENTRY TO THE TRANSFER MARKET DENIED":GOSUB 6630
8175 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
8180 LOCATE 7,5:PRINT "Your request for entry to the transfer market has been denied because you"
8185 PRINT "have a full quota of players in your squad.  If you further wish to enter the   transfer market, then you are required to decrease the size of your squad."
8190 PRINT "    This rule is explicit and you should not have attempted to contravene this  important item."
8195 PRINT:PRINT "                        Yours faithfully,
8200 PRINT:PRINT "                               Adrian Sill
8205 PRINT:PRINT "                                    (S.S.F.A Chairman)"
8210 GOSUB 6580
8215 RETURN
10000 REM +++++ EXAMINE THE TRANSFER MARKET
10005 I$="BUY"
10010 B=0:EMP=0:FOR A=20 TO 1 STEP -1:IF PLA$(TEAM%,A)<>"" THEN B=B+1 ELSE EMP=A
10020 NEXT A
10030 IF B=20 THEN GOSUB 8165:GOTO 530
10040 A$="EXAMINE THE TRANSFER MARKET":GOSUB 6630
10050 FOR A=1 TO 15:LOCATE A+5,1:PRINT SPACE$(80);:NEXT A
10060 IF DISPLAY%=2 THEN COLOR 9 ELSE COLOR 15
10070 LOCATE 4,28:PRINT "MONEY AVAILABLE  œ";MONEY-TOUT+TIN
10080 IF DISPLAY%=2 THEN COLOR 6 ELSE COLOR 15
10090 LOCATE 6,1:PRINT "      Player           Skill Age  Current Team           Position       Price"
10100 FOR A=1 TO 8:IF BID%(A)=3 OR TRANSTM%(A)=0 THEN TRANSTM%(A)=0:TRANSPL%(A)=0:GOTO 10140
10110 COLOR 15:LOCATE A+6,1:PRINT A:COLOR 14:LOCATE A+6,4:PRINT PLA$(TRANSTM%(A),TRANSPL%(A)):LOCATE A+6,25:PRINT FIG%(TRANSTM%(A),TRANSPL%(A),1):LOCATE A+6,29:PRINT FIG%(TRANSTM%(A),TRANSPL%(A),2):LOCATE A+6,35:PRINT TEAM$(TRANSTM%(A))
10120 LOCATE A+6,58:B=A
10130 PRINT POSITION$(FIG%(TRANSTM%(A),TRANSPL%(A),0)):LOCATE A+6,71:PRINT "œ";:IF PRICE(A)=0 THEN PRINT "FREE"; ELSE PRINT PRICE(A);
10140 NEXT A:B=B-1
10150 IF DISPLAY%=2 THEN COLOR 12 ELSE COLOR 15
10160 LOCATE 9+B,5:PRINT "Type the number of the player you wish to bid for OR"
10170 IF DISPLAY%=2 THEN LOCATE 10+B,5:PRINT "(";:COLOR 14:PRINT "Q";:COLOR 12:PRINT ")uit back to the transfer menu." ELSE COLOR 15:LOCATE 10+B,5:PRINT "(Q)uit back to the transfer menu."
10180 A$=INKEY$:IF A$="" THEN 10180
10190 IF A$="Q" OR A$="q" THEN GOTO 530
10200 IF VAL(A$)>.9 AND VAL(A$)<9.100001 THEN A%=VAL(A$):GOTO 10220
10210 GOTO 10180
10220 IF TRANSTM%(A%)=0 THEN GOTO 10180
10230 COLOR 15:LOCATE 12+B,5:INPUT "How much do you wish to bid ";BID
10240 LOCATE 15+B,20
10250 IF DISPLAY%=2 THEN COLOR 12 ELSE COLOR 15
10260 IF BID<0 THEN 10230 ELSE IF BID>MONEY-TOUT+TIN THEN PRINT "You don't have enough money":WHILE INKEY$="":WEND:LOCATE 8+B,42:PRINT "              ":GOTO 10230
10270 IF BID<BOTDOL(A%)+1 THEN PRINT "Your offer has been refused":WHILE INKEY$="":WEND:BID%(A%)=BID%(A%)+1:LOCATE 15+B,20:PRINT SPACE$(27);:GOTO 10050
10280 PRINT "Your offer has been accepted":FOR A=1 TO 1000:NEXT A
10290 TOUT=TOUT+BID
10300 SWAP PLA$(TRANSTM%(A%),TRANSPL%(A%)),PLA$(TEAM%,EMP):PRICE(A%)=0
10310 FOR A=0 TO 2:SWAP FIG%(TRANSTM%(A%),TRANSPL%(A%),A),FIG%(TEAM%,EMP,A):NEXT A:GOALS%(EMP)=0:TRANSTM%(A%)=0:TRANSPL%(A%)=0
10320 GOSUB 6580
10330 B=0:EMP=0:FOR A=16 TO 1 STEP -1:IF PLA$(TEAM%,A)<>"" THEN B=B+1 ELSE EMP=A
10340 NEXT A
10350 IF B=16 THEN GOTO 530 ELSE GOTO 10000
10360 RETURN
