10000 REM +++++ SELL A PLAYER
10005 I$="SELL"
10010 A$="SELL A PLAYER":GOSUB 6630
10020 COLOR 15:LOCATE 4,9:PRINT "NAME             POSITION   SKILL  AGE  GOALS":LOCATE 14,63:PRINT "MONEY":COLOR 10:LOCATE 15,62:PRINT INT(MONEY-TOUT+TIN):COLOR 15:LOCATE 15,62:PRINT "œ";
10030 R=1
10040 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
10050 LOCATE 6,58:PRINT "Use ARROW'S to move":LOCATE 7,60:PRINT "and press ENTER":LOCATE 8,56:PRINT "to select player to sell":LOCATE 10,59:PRINT "Press Q to Quit"
10060 FOR A=1 TO 20
10070 IF DISPLAY%=1 THEN GOTO 10100
10080 IF A=1 THEN COLOR 15 ELSE COLOR 10
10090 GOTO 10110
10100 IF A=1 THEN COLOR 15 ELSE COLOR 7
10110 LOCATE A+4,1:PRINT A;
10120 IF PLA$(TEAM%,A)<>"" THEN LOCATE A+4,5:PRINT PLA$(TEAM%,A)+SPACE$(10); ELSE GOTO 10160
10130 LOCATE A+4,25:PRINT POSITION$(FIG%(TEAM%,A,0))+SPACE$(5);
10140 FOR B=1 TO 2:LOCATE A+4,30+(B*7):PRINT FIG%(TEAM%,A,B);:NEXT B
10150 LOCATE A+4,50:PRINT GOALS%(A);
10160 NEXT A
10170 A$=INKEY$:IF A$="" THEN 10170
10180 IF A$=CHR$(13) AND PLA$(TEAM%,R)<>"" THEN 10290
10190 IF RIGHT$(A$,1)="H" THEN B=R:R=R-1 ELSE IF RIGHT$(A$,1)="P" THEN B=R:R=R+1
10200 IF A$="Q" OR A$="q" THEN GOTO 530
10210 IF R=0 THEN R=20 ELSE IF R=21 THEN R=1
10220 COLOR 15:LOCATE R+4,1:PRINT R;
10230 IF PLA$(TEAM%,R)="" THEN 10250
10240 LOCATE R+4,5:PRINT PLA$(TEAM%,R);:LOCATE R+4,25:PRINT POSITION$(FIG%(TEAM%,R,0));:FOR C=1 TO 2:LOCATE R+4,30+(C*7):PRINT FIG%(TEAM%,R,C);:NEXT C:LOCATE R+4,50:PRINT GOALS%(R);
10250 IF B=EXCH% THEN COLOR 4 ELSE COLOR 10
10260 LOCATE B+4,1:PRINT B;
10270 IF PLA$(TEAM%,B)="" THEN 10170
10280 LOCATE B+4,5:PRINT PLA$(TEAM%,B);:LOCATE B+4,25:PRINT POSITION$(FIG%(TEAM%,B,0));:FOR C=1 TO 2:LOCATE B+4,30+(C*7):PRINT FIG%(TEAM%,B,C);:NEXT C:LOCATE B+4,50:PRINT GOALS%(B);:GOTO 10170
10290 A=INT(RND*15)+1
10300 IF A=TEAM% THEN GOTO 10290
10310 B=INT(RND*4)+12
10320 IF PLA$(A,B)<>"" THEN GOTO 10290
10330 GOSUB 10450
10340 LOCATE 19,55:PRINT TEAM$(A);:LOCATE 20,55:PRINT " offer œ";:IF PRICE=0 THEN PRINT "FREE"; ELSE PRINT PRICE
10350 LOCATE 22,55:PRINT "Accept (Y)es or (N)o ?"
10360 A$=INKEY$:IF A$="" THEN 10360
10370 IF A$="Y" OR A$="y" THEN 10400
10380 IF A$="N" OR A$="n" THEN GOTO 10000
10390 GOTO 10360
10400 SWAP PLA$(A,B),PLA$(TEAM%,R)
10410 FOR C=0 TO 2:SWAP FIG%(A,B,C),FIG%(TEAM%,R,C):NEXT C
10420 GOALS%(R)=0:TIN=TIN+PRICE
10430 GOSUB 6580
10440 GOTO 10000
10450 REM CALCULATE AN OFFERED PRICE
10460 IF LEAGUE%=5 THEN PRICE=(FIG%(TEAM%,R,1)-20)*3000+(GOALS%(R)*3000)+(RND*5000)-(RND*4000)
10470 IF LEAGUE%=4 THEN PRICE=(FIG%(TEAM%,R,1)-30)*4000+(GOALS%(R)*4000)+(RND*7000)-(RND*5500)
10480 IF LEAGUE%=3 THEN PRICE=(FIG%(TEAM%,R,1)-40)*5500+(GOALS%(R)*5000)+(RND*9000)-(RND*7000)
10490 IF LEAGUE%=2 THEN PRICE=(FIG%(TEAM%,R,1)-50)*8500+(GOALS%(R)*8000)+(RND*30000)-(RND*20000)
10500 IF LEAGUE%=1 THEN PRICE=(FIG%(TEAM%,R,1)-66)*50000!+(GOALS%(R)*15000)+(RND*285000!)-(RND*285000!)
10510 IF PRICE>100000! THEN C=PRICE/100000!:D=INT(C):PRICE=D*100000! ELSE C=PRICE/1000:D=INT(C):PRICE=D*1000
10520 IF PRICE<1000 THEN PRICE=0
10530 RETURN
