10000 REM END OF SEASON ROUTINE
10005 I$="END"
10010 GOSUB 11680 ' END OF SEASON MESSAGE
10020 GOSUB 7920
10030 GOSUB 11770 ' STORE NAMES
10040 GOSUB 1720 ' SHOW LEAGUE TABLE
10050 GOSUB 4980 ' SHOW RESULTS
10060 GOSUB 4680 ' SHOW TOP SCORERS
10070 GOSUB 4830 ' PROGRESS CHART
10080 GOSUB 10370 ' ENS OF SEASON CALCULATIONS
10090 GOSUB 11500 ' MONEY AWARDS
10100 WEEK%=1
10110 GOSUB 7920 ' PLEASE WAIT
10120 GOTO 140
10370 REM ++++++++ END OF SEASON
10380 PROM%=0:RELE%=0
10390 SEASON%=SEASON%+1
10400 A$="END OF SEASON":GOSUB 6630
10410 FOR A=1 TO 16:IF POSITION%(A)=TEAM% THEN ENDPOS%=A
10420 NEXT
10430 COLOR 15:LOCATE 5,(50-(LEN(T$(TEAMNO%))))/2:IF LEAGUE%>1 THEN PRINT T$(TEAMNO%);" finished number";ENDPOS%;"in division";LEAGUE%-1 ELSE PRINT T$(TEAMNO%);" finished number ";ENDPOS%;" in the Premier League"
10440 IF ENDPOS%<4 THEN PROM%=1
10450 IF ENDPOS%>13 AND LEAGUE%<5 THEN RELE%=1 ELSE IF ENDPOS%=16 AND LEAGUE%=5 THEN RELE%=1
10460 IF PROM%=0 AND RELE%=0 THEN 10620
10470 C=1:A$=""
10480 WHILE A$="":A$=INKEY$
10490 IF DISPLAY%=2 THEN COLOR C ELSE COLOR 15
10500 IF PROM%=1 AND LEAGUE%=1 AND ENDPOS%=1 THEN LOCATE 8,32:PRINT "ENGLISH CHAMPIONS"
10510 IF PROM%=1 AND LEAGUE%=1 AND ENDPOS%=2 THEN LOCATE 8,31:PRINT "ENGLISH RUNNERS UP"
10520 IF PROM%=1 AND LEAGUE%=2 THEN LOCATE 8,22:PRINT "PROMOTION TO PREMIER LEAGUE ACHIEVED"
10530 IF PROM%=1 AND LEAGUE%>2 THEN LOCATE 8,21:PRINT "PROMOTION TO LEAGUE DIVISION ";LEAGUE%-2;" ACHIEVED"
10540 IF RELE%=1 AND LEAGUE%<5 THEN LOCATE 8,28:PRINT "RELEGATION TO DIVISION ";LEAGUE%;
10550 IF RELE%=1 AND LEAGUE%=5 THEN LOCATE 8,25:PRINT "RELEGATION FROM FOOTBALL LEAGUE"
10560 IF ENDPOS%=1 AND LEAGUE%>1 THEN LOCATE 10,36:PRINT "CHAMPIONS"
10570 IF ENDPOS%=2 AND LEAGUE%>1 THEN LOCATE 10,35:PRINT "RUNNERS UP"
10580 C=C+1:IF C>15 THEN C=1
10590 WEND
10600 GOSUB 6580
10610 IF RELE%=1 AND LEAGUE%=5 THEN GOTO 7920
10620 IF LEAGUE%=5 THEN GOTO 10650
10630 C=0:FOR A=1 TO 80:IF LEAGUE%(A)=LEAGUE%+1 THEN C=C+1:C(C)=A
10640 NEXT A
10650 IF LEAGUE%=1 THEN GOTO 10680
10660 D=0:FOR A=1 TO 80:IF LEAGUE%(A)=LEAGUE%-1 THEN D=D+1:B(D)=A
10670 NEXT A
10680 IF LEAGUE%=1 THEN GOTO 10750
10690 FOR A=1 TO 3
10700 LEAGUE%(TN%(POSITION%(A)))=LEAGUE%-1
10710 NEXT A
10720 FOR A=1 TO 3:A(A)=INT(RND*16)+1:NEXT
10730 IF A(1)=A(2) OR A(1)=A(3) OR A(2)=A(3) THEN 10720
10740 FOR A=1 TO 3:LEAGUE%(B(A(A)))=LEAGUE%:NEXT
10750 IF LEAGUE%=5 THEN GOTO 10820
10760 FOR A=14 TO 16
10770 LEAGUE%(TN%(POSITION%(A)))=LEAGUE%+1
10780 NEXT A
10790 FOR A=1 TO 3:A(A)=INT(RND*16)+1:NEXT
10800 IF A(1)=A(2) OR A(1)=A(3) OR A(2)=A(3) THEN 10790
10810 FOR A=1 TO 3:LEAGUE%(C(A(A)))=LEAGUE%:NEXT
10820 RETURN
11500 REM ++++++++ MONEY AWARDS
11510 SPECIAL%=0:A$="MONEY PROVIDED FOR THE NEW SEASON":GOSUB 6630
11520 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
11530 LOCATE 7,5:PRINT "You have been given an extra"
11540 A=(6-LEAGUE%)*100000!*(RND+.4)
11550 COLOR 15:LOCATE 9,30:PRINT A
11560 IF DISPLAY%=2 THEN COLOR 7 ELSE COLOR 15
11570 LOCATE 9,30:PRINT "œ";
11580 IF DISPLAY%=2 THEN COLOR 10 ELSE COLOR 15
11590 LOCATE 11,40:PRINT "for the new season."
11600 PRINT "     This sum is given in good faith that you will use it wisely and to         bring better fortune to our club. Do not spend hastily."
11610 LOCATE 14,5:PRINT "This brings the total amount of money available to yourself to :"
11620 MONEY=MONEY+A
11630 COLOR 15:LOCATE 16,30:PRINT MONEY
11640 IF DISPLAY%=2 THEN COLOR 7 ELSE COLOR 15
11650 LOCATE 16,30:PRINT "œ";
11660 GOSUB 6580
11670 RETURN
11680 REM ++++++++ END OF SEASON MESSAGE
11690 SPECIAL%=1:A$="END OF THE CURRENT SEASON":GOSUB 6630
11700 GOSUB 6580
11710 RETURN
11770 REM ***** STORE NAMES TO TEMPORARY NAMES FILE
11780 B=1:C=1
11790 OPEN "NAMES.TMP" FOR INPUT AS #1
11800 OPEN "NAMES.TP2" FOR OUTPUT AS #2
11810 WHILE B<81
11820 FOR A=1 TO 16
11830 INPUT #1,A$
11840 IF LEAGUE%(B)=LEAGUE% THEN A$=PLA$(C,A)
11850 PRINT #2,A$
11860 NEXT A
11870 IF LEAGUE%(B)=LEAGUE% THEN C=C+1
11880 B=B+1
11890 WEND
11900 CLOSE #1
11910 CLOSE #2
11920 RETURN
