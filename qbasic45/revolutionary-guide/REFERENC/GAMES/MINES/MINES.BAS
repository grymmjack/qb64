10 REM MINESWEEPER v1.0
20 REM Written by A.Sill
30 REM FOR WROX PRESS
40 REM (c) 1992
50 REM
60 SCREEN 9: CLS
70 A$ = "MINESWEEPER"
75 PATH$ = "A:\GAMES\MINES\"   ' Path for hi-score table
80 COLOR 2
90 LOCATE 2, (80 - LEN(A$)) / 2: PRINT A$
100 COLOR 10
110 COLOR 10: LOCATE 5, 1: PRINT "You are the captain of a minesweeping vessel for the Army and must clean out    your section of ocean in order to collect your pay.   Of course one mistake     will cost you and your crews' lives.   If you detect the"
120 PRINT "position of a mine, you must mark it by pressing M.   You detect the position   of a mine from the numbers given by adjacent squares.   If a square does not    contain a mine when detonated then a number will be shown.   This number tells"
130 PRINT "you how many of the adjacent 9 squares have a mine.   Good Luck."
140 RAND = 0
150 LOCATE 20, 33: COLOR 12: PRINT "Press any key"
160 A$ = ""
170 A$ = INKEY$: RAND = RAND + .1: IF A$ = "" THEN 170
180 RAND = RAND - VAL(RIGHT$(TIME$, 2))
190 RANDOMIZE RAND
200 MINES% = 200
210 DIM GRID%(21, 81), PICK%(21, 81), HI%(15), HI$(15)
220 GOTO 930
230 REM ###### INITIALISE ######
240 GOSUB 1540
250 CLS : PRINT "PLEASE WAIT"
260 PICKS% = 0: GOT% = 0: TM% = 0
270 FOR A = 1 TO 80: FOR B = 1 TO 20
280 GRID%(B, A) = 0: PICK%(B, A) = 0
290 NEXT B, A
300 FOR A = 1 TO MINES%
310 X% = INT(RND * 78) + 1: Y% = INT(RND * 19) + 1
320 IF GRID%(Y%, X%) = 1 THEN GOTO 310
330 GRID%(Y%, X%) = 1
340 NEXT A
350 CLS
360 FOR A = 1 TO 80
370 FOR B = 1 TO 20
380 LOCATE B, A: COLOR 7: PRINT "þ";
390 NEXT B, A
400 LOCATE 21, 1: COLOR 10: PRINT STRING$(80, "Û");
410 LOCATE 25, 1: COLOR 10: PRINT STRING$(80, "Û");
420 FOR A = 22 TO 24
430 LOCATE A, 1: PRINT "Û";
440 LOCATE A, 80: PRINT "Û";
450 NEXT A
460 LOCATE 21, 35: COLOR 14: PRINT "MINESWEEPER";
470 COLOR 7: LOCATE 23, 5: PRINT "Number of Mines : "; : COLOR 15: PRINT MINES%
480 COLOR 7: LOCATE 23, 40: PRINT "Number marked : "; : COLOR 15: PRINT PICKS%
490 COLOR 7: LOCATE 23, 65: PRINT "Time : "; : COLOR 15: PRINT TM%
500 COLOR 14: LOCATE 25, 35: PRINT "By THE ADE";
510 X% = 40: Y% = 10: TMSTART$ = TIME$
520 COLOR 4: LOCATE Y%, X%: PRINT "þ";
530 A$ = INKEY$: IF A$ = "" OR A$ = CHR$(0) THEN GOTO 530
540 DX% = X%: DY% = Y%
550 IF A$ = CHR$(0) + "H" AND Y% > 1 THEN Y% = Y% - 1
560 IF A$ = CHR$(0) + "P" AND Y% < 20 THEN Y% = Y% + 1
570 IF A$ = CHR$(0) + "M" AND X% < 80 THEN X% = X% + 1
580 IF A$ = CHR$(0) + "K" AND X% > 1 THEN X% = X% - 1
590 IF A$ = " " AND GRID%(Y%, X%) < 2 THEN GOSUB 710
600 IF (A$ = "M" OR A$ = "m") AND GRID%(Y%, X%) < 2 AND PICK%(Y%, X%) = 0 THEN PICK%(Y%, X%) = 1: PICKS% = PICKS% + 1: GOSUB 1480: COLOR 7: LOCATE 23, 40: PRINT "Number marked : "; : COLOR 15: PRINT PICKS%: GOTO 630
610 IF (A$ = "M" OR A$ = "m") AND GRID%(Y%, X%) < 2 AND PICK%(Y%, X%) = 1 THEN PICK%(Y%, X%) = 0: PICKS% = PICKS% - 1: GOSUB 1510: COLOR 7: LOCATE 23, 40: PRINT "Number marked : "; : COLOR 15: PRINT PICKS%
620 IF A$ = "G" THEN GOTO 1280
630 IF GOT% = MINES% AND PICKS% > MINES% THEN GOTO 1280
640 IF PICK%(DY%, DX%) = 0 AND GRID%(DY%, DX%) < 2 THEN COLOR 7: LOCATE DY%, DX%: PRINT "þ";
650 IF PICK%(DY%, DX%) = 0 AND GRID%(DY%, DX%) > 1 THEN COLOR 7: LOCATE DY%, DX%: PRINT RIGHT$(STR$(GRID%(DY%, DX%) - 3), 1);
660 IF PICK%(DY%, DX%) = 1 THEN COLOR 8: LOCATE DY%, DX%: PRINT "M";
670 IF PICK%(Y%, X%) = 0 AND GRID%(Y%, X%) < 2 THEN COLOR 4: LOCATE Y%, X%: PRINT "þ";
680 IF PICK%(Y%, X%) = 0 AND GRID%(Y%, X%) > 1 THEN COLOR 4: LOCATE Y%, X%: PRINT RIGHT$(STR$(GRID%(Y%, X%) - 3), 1);
690 IF PICK%(Y%, X%) = 1 THEN COLOR 4: LOCATE Y%, X%: PRINT "M";
700 GOTO 530
710 REM ##### UNCOVER SQUARE #####
720 IF GRID%(Y%, X%) = 1 THEN GOTO 1390
730 N% = 0
740 IF X% > 1 AND Y% < 20 AND (GRID%(Y% + 1, X% - 1) = 1) THEN N% = N% + 1
750 IF X% > 1 AND (GRID%(Y%, X% - 1) = 1) THEN N% = N% + 1
760 IF X% > 1 AND Y% > 1 AND (GRID%(Y% - 1, X% - 1) = 1) THEN N% = N% + 1
770 IF Y% > 1 AND (GRID%(Y% - 1, X%) = 1) THEN N% = N% + 1
780 IF Y% < 20 AND (GRID%(Y% + 1, X%) = 1) THEN N% = N% + 1
790 IF Y% > 1 AND X% < 80 AND (GRID%(Y% - 1, X% + 1) = 1) THEN N% = N% + 1
800 IF X% < 80 AND (GRID%(Y%, X% + 1) = 1) THEN N% = N% + 1
810 IF X% < 80 AND Y% < 20 AND (GRID%(Y% + 1, X% + 1) = 1) THEN N% = N% + 1
820 GRID%(Y%, X%) = 3 + N%
830 PICK%(Y%, X%) = 0
840 RETURN
850 REM ##### SHOW MINES #####
860 FOR A = 1 TO 80
870 FOR B = 1 TO 20
880 IF GRID%(B, A) = 1 AND PICK%(B, A) = 1 THEN LOCATE B, A: COLOR 14: PRINT "û";
890 IF GRID%(B, A) = 1 AND PICK%(B, A) = 0 THEN LOCATE B, A: COLOR 8: PRINT "H";
900 IF GRID%(B, A) = 0 AND PICK%(B, A) = 1 THEN LOCATE B, A: COLOR 4: PRINT "X";
910 NEXT B, A
920 RETURN
930 REM ##### MAIN SCREEN #####
940 CLS
950 LOCATE 1, 1: COLOR 1: PRINT STRING$(80, "Û");
960 LOCATE 2, 1: COLOR 9: PRINT STRING$(80, "Û");
970 LOCATE 3, 1: COLOR 11: PRINT STRING$(80, "Û");
980 LOCATE 23, 1: COLOR 11: PRINT STRING$(80, "Û");
990 LOCATE 24, 1: COLOR 9: PRINT STRING$(80, "Û");
1000 LOCATE 25, 1: COLOR 1: PRINT STRING$(80, "Û");
1010 LOCATE 5, 32: COLOR 15: PRINT "MINESWEEPER v1.0"
1020 LOCATE 8, 10: COLOR 7: PRINT "KEYS : "
1030 LOCATE 8, 25: COLOR 7: PRINT "Use "; : COLOR 11: PRINT "M"; : COLOR 7: PRINT " to mark a mine"
1040 LOCATE 9, 25: COLOR 7: PRINT "Use "; : COLOR 11: PRINT "SPACE"; : COLOR 7: PRINT " to uncover a square"
1050 LOCATE 19, 32: COLOR 9: PRINT "MINES :  "; MINES%
1060 LOCATE 12, 20: COLOR 7: PRINT "Increase number of mines        +"
1070 LOCATE 13, 20: PRINT "Decrease number of mines        -"
1080 LOCATE 14, 20: PRINT "See high-score table            H"
1090 LOCATE 15, 20: PRINT "Exit from Minesweeper           X"
1100 LOCATE 16, 20: PRINT "Play Minesweeper              RETURN"
1110 A$ = INKEY$: IF A$ = "" THEN GOTO 1110
1120 IF A$ = CHR$(13) THEN GOTO 230
1130 IF A$ = "H" OR A$ = "h" THEN GOSUB 1540: GOTO 930
1140 IF A$ = "+" AND MINES% < 400 THEN MINES% = MINES% + 10
1150 IF A$ = "-" AND MINES% > 20 THEN MINES% = MINES% - 10
1160 LOCATE 19, 32: COLOR 9: PRINT "MINES :  "; MINES%
1170 IF A$ = "X" THEN SYSTEM
1180 GOTO 1110
1190 REM ##### SCREEN LAYOUT #####
1200 CLS
1210 COLOR 2: LOCATE 1, 1: PRINT STRING$(80, "Û");
1220 LOCATE 25, 1: PRINT STRING$(80, "Û");
1230 FOR A = 2 TO 24: LOCATE A, 1: PRINT "Û"; : LOCATE A, 80: PRINT "Û"; : NEXT A
1240 COLOR 10: LOCATE 2, 2: PRINT STRING$(78, "Û");
1250 LOCATE 24, 2: PRINT STRING$(78, "Û");
1260 FOR A = 3 TO 23: LOCATE A, 2: PRINT "Û"; : LOCATE A, 79: PRINT "Û"; : NEXT A
1270 RETURN
1280 REM ##### WIN #####
1290 TMEND$ = TIME$
1300 GOSUB 2160
1310 GOSUB 1190
1320 COLOR 15: LOCATE 4, 20: PRINT "CONGRATULATIONS : YOU'VE CLEARED THE MINEFIELD"
1330 COLOR 8: LOCATE 6, 20: PRINT "Start Time : "; TMSTART$; "   End Time : "; TMEND$
1340 COLOR 7: LOCATE 8, 27: PRINT "Your time was : "; : COLOR 15: PRINT TM%; : COLOR 7: PRINT " seconds."
1350 WHILE INKEY$ <> "": WEND
1360 WHILE INKEY$ = "": WEND
1370 GOSUB 1840
1380 GOTO 220
1390 REM ##### LOSE #####
1400 GOSUB 850
1410 FOR A = 22 TO 24
1420 LOCATE A, 2: COLOR 4: PRINT STRING$(76, "Û");
1430 NEXT A
1440 COLOR 14: LOCATE 23, 33: PRINT "YOU'RE ALL DEAD"
1450 WHILE INKEY$ <> "": WEND
1460 WHILE INKEY$ = "": WEND
1470 GOTO 220
1480 REM ##### CHECK IF GOT ONE #####
1490 IF GRID%(Y%, X%) = 1 THEN GOT% = GOT% + 1
1500 RETURN
1510 REM ##### CHECK IF ONE REMOVED #####
1520 IF GRID%(Y%, X%) = 1 THEN GOT% = GOT% - 1
1530 RETURN
1540 REM ###### SHOW HIGH SCORE TABLE ######
1550 CLS
1560 LOCATE 1, 1: COLOR 1: PRINT STRING$(80, "Û");
1570 LOCATE 25, 1: PRINT STRING$(80, "Û");
1580 FOR A = 2 TO 24
1590 LOCATE A, 1: PRINT "Û";
1600 LOCATE A, 80: PRINT "Û";
1610 NEXT A
1620 LOCATE 2, 2: COLOR 9: PRINT STRING$(78, "Û");
1630 LOCATE 24, 2: PRINT STRING$(78, "Û");
1640 FOR A = 2 TO 24
1650 LOCATE A, 2: PRINT "Û";
1660 LOCATE A, 79: PRINT "Û";
1670 NEXT A
1680 LOCATE 4, 27: COLOR 15: PRINT "MINESWEEPER HI-SCORE TABLE"
1690 COLOR 7: LOCATE 6, 36: PRINT MINES%; "Mines"
1700 FLAG% = 1
1710 FL$ = PATH$ + "MINES" + RIGHT$(STR$(MINES%), LEN(STR$(MINES%)) - 1) + ".HIS"
1720 OPEN FL$ FOR INPUT AS #1
1730 FOR A = 1 TO 15
1740 INPUT #1, HI$(A)
1750 INPUT #1, HI%(A)
1760 LOCATE 7 + A, 12: PRINT STRING$(55, "_");
1770 LOCATE 7 + A, 12: PRINT A
1780 LOCATE 7 + A, 18: PRINT " "; HI$(A)
1790 LOCATE 7 + A, 68: PRINT HI%(A)
1800 NEXT A
1810 CLOSE #1
1820 WHILE INKEY$ = "": WEND
1830 RETURN
1840 REM ###### UPDATE HI-SCORE TABLE ######
1850 VAR% = 0
1860 FOR A = 15 TO 1 STEP -1
1870 IF TM% < HI%(A) THEN VAR% = A
1880 NEXT A
1890 IF VAR% = 0 THEN GOTO 2150
1900 CLS
1910 LOCATE 1, 1: COLOR 9: PRINT STRING$(80, "Û");
1920 LOCATE 25, 1: PRINT STRING$(80, "Û");
1930 FOR A = 2 TO 24
1940 LOCATE A, 1: PRINT "Û";
1950 LOCATE A, 80: PRINT "Û";
1960 NEXT A
1970 COLOR 14: LOCATE 3, 15: PRINT "CONGRATULATIONS YOUR EFFORTS HAVE BEEN REWARDED"
1980 COLOR 7: LOCATE 6, 5: PRINT "Your name must now be engraved upon the high time trophy for"
1990 LOCATE 7, 5: PRINT "'Minesweeper'.   Please enter how you wish it to be represented : "
2000 FOR A = 14 TO VAR% STEP -1
2010 HI$(A + 1) = HI$(A)
2020 HI%(A + 1) = HI%(A)
2030 NEXT A
2040 HI%(VAR%) = TM%
2050 COLOR 15: LOCATE 10, 15: INPUT "Enter your name : "; HISIG$
2060 IF LEN(HISIG$) > 25 THEN HISIG$ = LEFT$(HISIG$, 25)
2070 HI$(VAR%) = HISIG$
2080 FL$ = PATH$ + "MINES" + RIGHT$(STR$(MINES%), LEN(STR$(MINES%)) - 1) + ".HIS"
2090 OPEN FL$ FOR OUTPUT AS #1
2100 FOR A = 1 TO 15
2110 WRITE #1, HI$(A)
2120 WRITE #1, HI%(A)
2130 NEXT A
2140 CLOSE #1
2150 RETURN
2160 REM ##### CALCULATE TIME SPENT #####
2170 STARTSECS% = VAL(RIGHT$(TMSTART$, 2))
2180 STARTMINS% = VAL(MID$(TMSTART$, 4, 2))
2190 STARTHRS% = VAL(LEFT$(TMSTART$, 2))
2200 REM PRINT STARTSECS%,STARTMINS%,STARTHRS%
2210 ENDSECS% = VAL(RIGHT$(TMEND$, 2))
2220 ENDMINS% = VAL(MID$(TMEND$, 4, 2))
2230 ENDHRS% = VAL(LEFT$(TMEND$, 2))
2240 REM PRINT ENDSECS%,ENDMINS%,ENDHRS%
2250 TM% = 0
2260 IF ENDHRS% = STARTHRS% AND ENDMINS% > STARTMINS% AND ENDSECS% > STARTSECS% THEN TM% = TM% + (60 * (ENDMINS% - STARTMINS%)) + (ENDSECS% - STARTSECS%)
2270 IF ENDHRS% = STARTHRS% AND ENDMINS% = STARTMINS% AND ENDSECS% > STARTSECS% THEN TM% = TM% + (ENDSECS% - STARTSECS%)
2280 IF ENDHRS% = STARTHRS% AND ENDMINS% > STARTMINS% AND ENDSECS% < STARTSECS% THEN TM% = TM% + (60 * (ENDMINS% - STARTMINS%)) + (ENDSECS% - STARTSECS%)
2290 IF ENDHRS% = STARTHRS% AND ENDMINS% > STARTMINS% AND ENDSECS% = STARTSECS% THEN TM% = TM% + (60 * (ENDMINS% - STARTMINS%))
2300 IF ENDHRS% > STARTHRS% AND ENDMINS% < STARTMINS% AND ENDSECS% > STARTSECS% THEN TM% = TM% + (60 * ((60 - STARTMINS%) + ENDMINS%)) + (ENDSECS% - STARTSECS%)
2310 IF ENDHRS% > STARTHRS% AND ENDMINS% < STARTMINS% AND ENDSECS% < STARTSECS% THEN TM% = TM% + (60 * (((60 - STARTMINS%) + ENDMINS%) - 1)) + ((60 - STARTSECS%) + ENDSECS%)
2320 RETURN

